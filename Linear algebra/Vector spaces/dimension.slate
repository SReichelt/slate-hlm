%../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Dimension_(vector_space)
 *             * https://mathworld.wolfram.com/Dimension.html
 *             * https://proofwiki.org/wiki/Definition:Dimension_of_Vector_Space
 *             * https://ncatlab.org/nlab/show/dimension#of_spaces
 *             * https://leanprover-community.github.io/mathlib_docs/linear_algebra/finite_dimensional.html#finite_dimensional.findim
 */
$dimension(
  𝐅: %Element(
    $../../Algebra/Fields/Fields,
    auto = %true
  ),
  𝐕: %Element($"Vector spaces"(𝐅 = {𝐅})),
  _1: %Constraint($"finite-dimensional"(
    𝐅 = {𝐅},
    𝐕 = {𝐕}
  ))
): %ImplicitOperator {
  notation = [$Function(
    function = 'dim',
    arguments = [𝐕]
  )],
  parameter = #(n: %Element($../../Essentials/Numbers/Natural/"Natural numbers")),
  definition = [
    %forall(
      #(𝒙: %Element($../../Algebra/Rings/Modules/"Left modules"/Coordinates/Bases(
        𝐑 = {𝐅},
        𝐌 = {𝐕}
      ))),
      %equals(
        $../../Essentials/Sequences/"ordinal-indexed sequence length"(
          S = {$../../Algebra/Rings/Modules/"Left modules"/Carrier(
            𝐑 = {𝐅},
            𝐌 = {𝐕}
          )},
          𝒂 = {𝒙}
        ),
        n
      )
    ),
    %exists(
      #(𝒙: %Element($../../Algebra/Rings/Modules/"Left modules"/Coordinates/Bases(
        𝐑 = {𝐅},
        𝐌 = {𝐕}
      ))),
      formula = %equals(
        $../../Essentials/Sequences/"ordinal-indexed sequence length"(
          S = {$../../Algebra/Rings/Modules/"Left modules"/Carrier(
            𝐑 = {𝐅},
            𝐌 = {𝐕}
          )},
          𝒂 = {𝒙}
        ),
        n
      )
    ),
    %forall(
      #(
        𝒙: %Element($../../Algebra/Rings/Modules/"Left modules"/Coordinates/"Element sequences"(
          𝐑 = {𝐅},
          𝐌 = {𝐕}
        )),
        _1: %Constraint(%setEquals(
          $../../Algebra/Rings/Modules/"Left modules"/Coordinates/Span(
            𝐑 = {𝐅},
            𝐌 = {𝐕},
            𝒙 = {𝒙}
          ),
          $../../Algebra/Rings/Modules/"Left modules"/Carrier(
            𝐑 = {𝐅},
            𝐌 = {𝐕}
          )
        ))
      ),
      $../../Essentials/Numbers/Ordinal/"less or equal"(
        β = {$../../Essentials/Sequences/"ordinal-indexed sequence length"(
          S = {$../../Algebra/Rings/Modules/"Left modules"/Carrier(
            𝐑 = {𝐅},
            𝐌 = {𝐕}
          )},
          𝒂 = {𝒙}
        )},
        α = {n}
      )
    )
  ]
}
