%../../../../../logics/hlm%

/**
 * @remarks The generalized Cartesian product over an index set can be constructed in HLM via its integrated bound variable concept. It is essentially the same as a set of dependent functions.
 *
 * @references * https://en.wikipedia.org/wiki/Cartesian_product#Infinite_Cartesian_products
 *             * https://proofwiki.org/wiki/Definition:Cartesian_Product/Family_of_Sets
 *             * https://ncatlab.org/nlab/show/cartesian+product#in_sets
 */
$"Generalized cartesian product"(
  I: %Set,
  _1: %Binder(
    #(i: %Element(I)),
    #(S: %Set(auto = %true))
  )
): %Construction {
  $product(_2: %Binder(
    #(j: %Element(I)),
    #(s: %Element(S[i = j]))
  )): %Constructor {
    notation = $SubSup(
      body = $Parens(body = s, style = '()'),
      sub = $ElementRelation(operands = [j, I])
    ),
    equalityDefinition = {
      leftParameters = #(_2: %Binder(
        #(j: %Element(I)),
        #(s: %Element(S[i = j]))
      )),
      rightParameters = #(_3: %Binder(
        #(j: %Element(I)),
        #(t: %Element(S[i = j]))
      )),
      definition = [%forall(
        #(j: %Element(I)),
        %equals(
          s[j = j],
          t[j = j]
        )
      )],
      isomorphic = %true
    }
  }

  notation = $LargeOperator(
    symbol = '‚àè',
    operand = S,
    under = $ElementRelation(operands = [i, I])
  )
}
