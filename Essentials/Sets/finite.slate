%../../../../logics/hlm%

[
  $~Bijections = $../Functions/Bijections,
  $~Cardinal = $../Numbers/Cardinal,
  $~"Cardinal numbers" = $../Numbers/Cardinal/"Cardinal numbers",
  $~Functions = $../Functions/Functions,
  $~"Generalized restriction preserves injectivity" = $../Functions/"Generalized restriction preserves injectivity",
  $~"Natural numbers" = $../Numbers/Natural/"Natural numbers",
  $~"Segment (less or equal)" = $../Numbers/Natural/Subsets/"Segment (less or equal)",
  $~"Segment (less)" = $../Numbers/Natural/Subsets/"Segment (less)",
  $~finite = $../Numbers/Cardinal/finite,
  $~"generalized restriction" = $../Functions/"generalized restriction",
  $~injective = $../Functions/injective,
  $~"less or equal" = $../Numbers/Cardinal/"less or equal",
  $~number = $../Numbers/Natural/number,
  $~sum = $../Numbers/Natural/sum
]

/**
 * @remarks This definition uses functions as well as the natural and cardinal numbers, all of which are defined in later sections (see `$../Functions/Functions`, `$../Numbers/Natural/"Natural numbers"`, `$../Numbers/Cardinal/"Cardinal numbers"`). That is not a problem as long as no circularities exist. In other words, definitions and theorems are ordered by topic instead of by dependency.
 *
 * @references * https://en.wikipedia.org/wiki/Finite_set
 *             * https://mathworld.wolfram.com/FiniteSet.html
 *             * https://proofwiki.org/wiki/Definition:Finite_Set
 *             * https://ncatlab.org/nlab/show/finite+set
 *             * https://leanprover-community.github.io/mathlib_docs/data/set/finite.html#set.finite
 */
$finite(S: %Set): %Predicate {
  notation = $Property(
    operand = S,
    property = %neg('finite', 'infinite')
  ),
  definition = [
    $~finite(κ = $~"Cardinal numbers".cardinality(S = S)),
    %exists(#(
      k: %Element($~"Natural numbers"),
      f: %Element($~Bijections(
        X = S,
        Y = $~"Segment (less)"(n = k)
      ))
    )),
    %exists(#(
      l: %Element($~"Natural numbers"),
      g: %Element($~Bijections(
        X = $~"Segment (less)"(n = l),
        Y = S
      ))
    )),
    %exists(
      #(
        m: %Element($~"Natural numbers"),
        h: %Element($~Functions(
          X = S,
          Y = $~"Segment (less)"(n = m)
        ))
      ),
      formula = $~injective(
        X = S,
        Y = $~"Segment (less)"(n = m),
        f = h
      )
    ),
    %exists(
      #(
        n: %Element($~"Natural numbers"),
        i: %Element($~Functions(
          X = S,
          Y = $~"Segment (less or equal)"(n = n)
        ))
      ),
      formula = $~injective(
        X = S,
        Y = $~"Segment (less or equal)"(n = n),
        f = i
      )
    )
  ],
  equivalenceProofs = [
    {
      from = 1,
      to = 2,
      parameters = #(_1: %Constraint($~finite(κ = $~"Cardinal numbers".cardinality(S = S)))),
      goal = %exists(#(
        k: %Element($~"Natural numbers"),
        f: %Element($~Bijections(
          X = S,
          Y = $~"Segment (less)"(n = k)
        ))
      )),
      steps = #(
        _: %Consider(_1),
        _: %UseDef(result = %exists(
          #(n: %Element($~"Natural numbers")),
          formula = %equals(
            $~"Cardinal numbers".cardinality(S = S),
            n
          )
        )),
        _: %UseExists(#(n: %Element($~"Natural numbers"))),
        _: %UseDef(result = %exists(#(r: %Element($~Bijections(
          X = S,
          Y = $~"Segment (less)"(n = n)
        ))))),
        _: %UseExists(#(r: %Element($~Bijections(
          X = S,
          Y = $~"Segment (less)"(n = n)
        )))),
        _: %ProveExists({k = n, f = r})
      )
    },
    {
      from = 2,
      to = 3,
      parameters = #(
        k: %Element($~"Natural numbers"),
        f: %Element($~Bijections(
          X = S,
          Y = $~"Segment (less)"(n = k)
        ))
      ),
      goal = %exists(#(
        l: %Element($~"Natural numbers"),
        g: %Element($~Bijections(
          X = $~"Segment (less)"(n = l),
          Y = S
        ))
      )),
      steps = #(_: %ProveExists({
        l = k,
        g = $../Functions/inverse(
          X = S,
          Y = $~"Segment (less)"(n = k),
          f = f
        )
      }))
    },
    {
      from = 3,
      to = 1,
      parameters = #(
        l: %Element($~"Natural numbers"),
        g: %Element($~Bijections(
          X = $~"Segment (less)"(n = l),
          Y = S
        ))
      ),
      goal = $~finite(κ = $~"Cardinal numbers".cardinality(S = S)),
      steps = #(_: %ProveDef(proof = {
        goal = %in(
          $~"Cardinal numbers".cardinality(S = S),
          $~"Natural numbers"
        ),
        steps = #(
          _1: %State(
            %equals(
              l,
              $~"Cardinal numbers".cardinality(S = S)
            ),
            proof = {steps = #(_: %ProveDef(proof = {
              goal = %exists(#(s: %Element($~Bijections(
                X = $~"Segment (less)"(n = l),
                Y = S
              )))),
              steps = #(_: %ProveExists({s = g}))
            }))}
          ),
          _: %Consider(l),
          _: %Substitute(
            #(_: %Consider(_1)),
            1,
            %in(
              $~"Cardinal numbers".cardinality(S = S),
              $~"Natural numbers"
            )
          )
        )
      }))
    },
    {
      from = 2,
      to = 4,
      parameters = #(
        k: %Element($~"Natural numbers"),
        f: %Element($~Bijections(
          X = S,
          Y = $~"Segment (less)"(n = k)
        ))
      ),
      goal = %exists(
        #(
          m: %Element($~"Natural numbers"),
          h: %Element($~Functions(
            X = S,
            Y = $~"Segment (less)"(n = m)
          ))
        ),
        formula = $~injective(
          X = S,
          Y = $~"Segment (less)"(n = m),
          f = h
        )
      ),
      steps = #(
        _: %Consider(f),
        _: %UseDef(result = $../Functions/bijective(
          X = S,
          Y = $~"Segment (less)"(n = k),
          f = f
        )),
        _: %UseDef(result = $~injective(
          X = S,
          Y = $~"Segment (less)"(n = k),
          f = f
        )),
        _: %ProveExists({m = k, h = f})
      )
    },
    {
      from = 4,
      to = 1,
      parameters = #(
        n: %Element($~"Natural numbers"),
        i: %Element($~Functions(
          X = S,
          Y = $~"Segment (less)"(n = n)
        )),
        _1: %Constraint($~injective(
          X = S,
          Y = $~"Segment (less)"(n = n),
          f = i
        ))
      ),
      goal = $~finite(κ = $~"Cardinal numbers".cardinality(S = S)),
      steps = #(_: %ProveDef(proof = {
        goal = %exists(
          #(y: %Element($~"Natural numbers")),
          formula = $~"less or equal"(
            κ = $~"Cardinal numbers".cardinality(S = S),
            μ = y
          )
        ),
        steps = #(_: %ProveExists(
          {y = n},
          proof = {
            goal = $~"less or equal"(
              κ = $~"Cardinal numbers".cardinality(S = S),
              μ = n
            ),
            steps = #(_: %ProveDef(proof = {
              goal = %exists(
                #(φ: %Element($~Cardinal/Homomorphisms/Homomorphisms(
                  κ = $~"Cardinal numbers".cardinality(S = S),
                  μ = n
                ))),
                formula = $~injective(
                  X = S,
                  Y = $~Cardinal/Carrier(κ = n),
                  f = φ
                )
              ),
              steps = #(_: %ProveExists({φ = i}))
            }))
          }
        ))
      }))
    },
    {
      from = 4,
      to = 5,
      parameters = #(
        m: %Element($~"Natural numbers"),
        h: %Element($~Functions(
          X = S,
          Y = $~"Segment (less)"(n = m)
        )),
        _1: %Constraint($~injective(
          X = S,
          Y = $~"Segment (less)"(n = m),
          f = h
        ))
      ),
      goal = %exists(
        #(
          n: %Element($~"Natural numbers"),
          i: %Element($~Functions(
            X = S,
            Y = $~"Segment (less or equal)"(n = n)
          ))
        ),
        formula = $~injective(
          X = S,
          Y = $~"Segment (less or equal)"(n = n),
          f = i
        )
      ),
      steps = #(
        _: %UseTheorem(
          $../Numbers/Natural/Subsets/"Exclusive initial segment is subset of inclusive segment"(n = m),
          result = %sub(
            $~"Segment (less)"(n = m),
            $~"Segment (less or equal)"(n = m)
          )
        ),
        _: %ProveExists(
          {
            n = m,
            i = $~"generalized restriction"(
              X = S, A = S,
              B = $~"Segment (less or equal)"(n = m),
              Y = $~"Segment (less)"(n = m),
              f = h
            )
          },
          proof = {
            goal = $~injective(
              X = S,
              Y = $~"Segment (less or equal)"(n = m),
              f = $~"generalized restriction"(
                X = S, A = S,
                B = $~"Segment (less or equal)"(n = m),
                Y = $~"Segment (less)"(n = m),
                f = h
              )
            ),
            steps = #(_: %UseTheorem(
              $~"Generalized restriction preserves injectivity"(
                X = S, A = S,
                B = $~"Segment (less or equal)"(n = m),
                Y = $~"Segment (less)"(n = m),
                f = h
              ),
              result = $~injective(
                X = S,
                Y = $~"Segment (less or equal)"(n = m),
                f = $~"generalized restriction"(
                  X = S, A = S,
                  B = $~"Segment (less or equal)"(n = m),
                  Y = $~"Segment (less)"(n = m),
                  f = h
                )
              )
            ))
          }
        )
      )
    },
    {
      from = 5,
      to = 4,
      parameters = #(
        n: %Element($~"Natural numbers"),
        i: %Element($~Functions(
          X = S,
          Y = $~"Segment (less or equal)"(n = n)
        )),
        _1: %Constraint($~injective(
          X = S,
          Y = $~"Segment (less or equal)"(n = n),
          f = i
        ))
      ),
      goal = %exists(
        #(
          m: %Element($~"Natural numbers"),
          h: %Element($~Functions(
            X = S,
            Y = $~"Segment (less)"(n = m)
          ))
        ),
        formula = $~injective(
          X = S,
          Y = $~"Segment (less)"(n = m),
          f = h
        )
      ),
      steps = #(_: %ProveExists(
        {
          m = $~sum(
            m = n,
            n = $~number(value = 1)
          ),
          h = $~"generalized restriction"(
            X = S, A = S,
            B = $~"Segment (less)"(n = $~sum(
              m = n,
              n = $~number(value = 1)
            )),
            Y = $~"Segment (less or equal)"(n = n),
            f = i
          )
        },
        proof = {
          goal = $~injective(
            X = S,
            Y = $~"Segment (less)"(n = $~sum(
              m = n,
              n = $~number(value = 1)
            )),
            f = $~"generalized restriction"(
              X = S, A = S,
              B = $~"Segment (less)"(n = $~sum(
                m = n,
                n = $~number(value = 1)
              )),
              Y = $~"Segment (less or equal)"(n = n),
              f = i
            )
          ),
          steps = #(_: %UseTheorem(
            $~"Generalized restriction preserves injectivity"(
              X = S, A = S,
              B = $~"Segment (less)"(n = $~sum(
                m = n,
                n = $~number(value = 1)
              )),
              Y = $~"Segment (less or equal)"(n = n),
              f = i
            ),
            result = $~injective(
              X = S,
              Y = $~"Segment (less)"(n = $~sum(
                m = n,
                n = $~number(value = 1)
              )),
              f = $~"generalized restriction"(
                X = S, A = S,
                B = $~"Segment (less)"(n = $~sum(
                  m = n,
                  n = $~number(value = 1)
                )),
                Y = $~"Segment (less or equal)"(n = n),
                f = i
              )
            )
          ))
        }
      ))
    }
  ]
}
