%../../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Rational_number#Ordering
 *             * https://coq.inria.fr/library/Coq.QArith.QArith_base.html#Qle
 *             * https://leanprover-community.github.io/mathlib_docs/data/rat/order.html#rat.le
 */
$"less or equal"(
  a: %Element($"Rational numbers"),
  b: %Element(%previous)
): %Predicate {
  properties = {negation = $less(
    b = {a},
    a = {b}
  )},
  notation = $LessOrEqualRelation(operands = [a, b]),
  definition = [%structural(
    b,
    $"Rational numbers",
    [{
      constructor = $"Rational numbers".quotient,
      parameters = #(
        n_b: %Element($../Integer/Integers),
        d_b: %Element($../Natural/Subsets/"Positive numbers")
      ),
      value = %structural(
        a,
        $"Rational numbers",
        [{
          constructor = $"Rational numbers".quotient,
          parameters = #(
            n_a: %Element($../Integer/Integers),
            d_a: %Element($../Natural/Subsets/"Positive numbers")
          ),
          value = $../Integer/"less or equal"(
            a = {$../Integer/product(
              a = {n_a},
              b = {d_b}
            )},
            b = {$../Integer/product(
              a = {n_b},
              b = {d_a}
            )}
          )
        }]
      )
    }]
  )]
}
