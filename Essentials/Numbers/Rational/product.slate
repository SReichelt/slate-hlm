%../../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Rational_number#Multiplication
 *             * https://proofwiki.org/wiki/Definition:Multiplication/Rational_Numbers
 *             * https://coq.inria.fr/library/Coq.QArith.QArith_base.html#Qmult
 *             * https://leanprover-community.github.io/mathlib_docs/data/rat/basic.html#rat.mul
 */
$product(
  a: %Element($"Rational numbers"),
  b: %Element(%previous)
): %ExplicitOperator {
  notation = [$MultiplicationOperator(operands = [a, b])],
  definition = [%structuralCases(
    b,
    $"Rational numbers",
    [{
      constructor = $"Rational numbers".quotient,
      parameters = #(
        n_b: %Element($../Integer/Integers),
        d_b: %Element($../Natural/Subsets/"Positive numbers")
      ),
      value = %structuralCases(
        a,
        $"Rational numbers",
        [{
          constructor = $"Rational numbers".quotient,
          parameters = #(
            n_a: %Element($../Integer/Integers),
            d_a: %Element($../Natural/Subsets/"Positive numbers")
          ),
          value = $"Rational numbers".quotient(
            n = {$../Integer/product(
              a = {n_a},
              b = {n_b}
            )},
            d = {$../Natural/product(
              m = {d_a},
              n = {d_b}
            )}
          )
        }]
      )
    }]
  )]
}
