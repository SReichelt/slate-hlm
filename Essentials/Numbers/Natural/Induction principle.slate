%../../../../../logics/hlm%

/**
 * @remarks Structural induction on the natural numbers is a direct consequence of their definition as a construction. This set-theoretic form of the induction principle is provable from it.
 *
 * @references * https://proofwiki.org/wiki/Principle_of_Mathematical_Induction/Well-Ordered_Set
 */
$"Induction principle"(M: %Subset($"Natural numbers")): %EquivalenceTheorem {
  conditions = [
    $inductive(M = M),
    %setEquals(M, $"Natural numbers")
  ],
  equivalenceProofs = [
    {
      from = 1,
      to = 2,
      parameters = #(_1: %Constraint($inductive(M = M))),
      goal = %setEquals(M, $"Natural numbers"),
      steps = #(_: %ProveEquivalence([
        {
          from = 1,
          to = 2,
          steps = #()
        },
        {
          from = 2,
          to = 1,
          steps = #(_: %ProveDef(proof = {
            goal = %forall(
              #(n: %Element($"Natural numbers")),
              %in(n, M)
            ),
            steps = #(_: %ProveForAll({
              parameters = #(n: %Element($"Natural numbers")),
              goal = %in(n, M),
              steps = #(_: %UseTheorem(
                $"Induction lemma"(M = M, n = n),
                result = %in(n, M)
              ))
            }))
          }))
        }
      ]))
    },
    {
      from = 2,
      to = 1,
      parameters = #(_1: %Constraint(%setEquals(M, $"Natural numbers"))),
      goal = $inductive(M = M),
      steps = #(
        _: %State(
          $inductive(M = $"Natural numbers"),
          proof = {steps = #(_: %ProveDef)}
        ),
        _: %Substitute(
          #(_: %Consider(_1)),
          2,
          $inductive(M = M)
        )
      )
    }
  ]
}
