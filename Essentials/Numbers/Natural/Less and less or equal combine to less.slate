%../../../../../logics/hlm%

[
  $~"Positive numbers" = $Subsets/"Positive numbers"
]

/**
 * @references * https://leanprover-community.github.io/mathlib_docs/init/data/nat/basic.html#nat.lt_of_lt_of_le
 */
$"Less and less or equal combine to less"(
  a,b,c: %Element($"Natural numbers"),
  _1: %Constraint($less(m = a, n = b)),
  _2: %Constraint($"less or equal"(
    m = %associative(b),
    n = c
  ))
): %StandardTheorem {
  claim = $less(m = a, n = c),
  proofs = [{steps = #(_: %ProveDef(proof = {
    goal = %exists(
      #(x: %Element($~"Positive numbers")),
      formula = %equals(
        $sum(m = a, n = x),
        c
      )
    ),
    steps = #(
      _: %Consider(_1),
      _: %UseDef(result = %exists(
        #(y: %Element($~"Positive numbers")),
        formula = %equals(
          $sum(m = a, n = y),
          b
        )
      )),
      _: %UseExists(#(y: %Element($~"Positive numbers"))),
      _: %Consider(_2),
      _: %UseDef(result = %exists(
        #(z: %Element($"Natural numbers")),
        formula = %equals(
          $sum(
            m = %associative(b),
            n = z
          ),
          c
        )
      )),
      _: %UseExists(#(z: %Element($"Natural numbers"))),
      _: %ProveExists({x = $sum(m = y, n = z)})
    )
  }))}]
}
