%../../../../../logics/hlm%

$"Sum is commutative lemma"(m,n: %Element($"Natural numbers")): %StandardTheorem {
  claim = %equals(
    $sum(m = m, n = n),
    $sum(m = n, n = m)
  ),
  proofs = [{steps = #(_: %ProveByInduction(
    n, $"Natural numbers",
    [
      {
        constructor = $"Natural numbers".zero,
        value = {
          goal = %equals(
            m,
            $sum(
              m = $"Natural numbers".zero,
              n = m
            )
          ),
          steps = #(_: %UseTheorem(
            $"Left-addition of zero is neutral"(n = m),
            result = %equals(
              $sum(
                m = $"Natural numbers".zero,
                n = m
              ),
              m
            )
          ))
        }
      },
      {
        constructor = $"Natural numbers".successor,
        parameters = #(x: %Element($"Natural numbers")),
        value = {
          goal = %equals(
            $"Natural numbers".successor(n = $sum(m = m, n = x)),
            $sum(
              m = $"Natural numbers".successor(n = x),
              n = m
            )
          ),
          steps = #(
            _1: %UseTheorem(
              $"Sum is commutative lemma"(m = m, n = x),
              result = %equals(
                $sum(m = m, n = x),
                $sum(m = x, n = m)
              )
            ),
            _: %ProveBySubstitution(
              _1, 1,
              %equals(
                $"Natural numbers".successor(n = $sum(m = x, n = m)),
                $sum(
                  m = $"Natural numbers".successor(n = x),
                  n = m
                )
              ),
              proof = {steps = #(_: %UseTheorem(
                $"Successor can be pulled over addition"(m = x, n = m),
                result = %equals(
                  $sum(
                    m = $"Natural numbers".successor(n = x),
                    n = m
                  ),
                  $"Natural numbers".successor(n = $sum(m = x, n = m))
                )
              ))}
            )
          )
        }
      }
    ]
  ))}]
}
