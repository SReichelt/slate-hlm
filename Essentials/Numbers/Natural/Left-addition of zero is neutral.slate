%../../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Proofs_involving_the_addition_of_natural_numbers#Proof_of_identity_element
 *             * https://proofwiki.org/wiki/Identity_Element_of_Natural_Number_Addition_is_Zero
 *             * https://proofwiki.org/wiki/Natural_Number_Addition_Commutes_with_Zero
 *             * https://coq.inria.fr/library/Coq.Arith.PeanoNat.html#Nat.add_0_l
 *             * https://coq.inria.fr/library/Coq.NArith.BinNat.html#N.add_0_l
 *             * https://leanprover-community.github.io/mathlib_docs/core/init/data/nat/basic.html#nat.zero_add
 */
$"Left-addition of zero is neutral"(n: %Element($"Natural numbers")): %StandardTheorem {
  claim = %equals(
    $sum(
      m = $"Natural numbers".zero,
      n = n
    ),
    n
  ),
  proofs = [{steps = #(_: %ProveByInduction(
    n, $"Natural numbers",
    [
      {
        constructor = $"Natural numbers".zero,
        value = {
          goal = %equals(
            $"Natural numbers".zero,
            $"Natural numbers".zero
          ),
          steps = #()
        }
      },
      {
        constructor = $"Natural numbers".successor,
        parameters = #(x: %Element($"Natural numbers")),
        value = {
          goal = %equals(
            $"Natural numbers".successor(n = $sum(
              m = $"Natural numbers".zero,
              n = x
            )),
            $"Natural numbers".successor(n = x)
          ),
          steps = #(
            _1: %UseTheorem(
              $"Left-addition of zero is neutral"(n = x),
              result = %equals(
                $sum(
                  m = $"Natural numbers".zero,
                  n = x
                ),
                x
              )
            ),
            _: %State(%equals(
              $"Natural numbers".successor(n = x),
              $"Natural numbers".successor(n = x)
            )),
            _: %Substitute(
              #(_: %Consider(_1)),
              2,
              %equals(
                $"Natural numbers".successor(n = $sum(
                  m = $"Natural numbers".zero,
                  n = x
                )),
                $"Natural numbers".successor(n = x)
              )
            )
          )
        }
      }
    ]
  ))}]
}
