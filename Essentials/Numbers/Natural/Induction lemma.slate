%../../../../../logics/hlm%

/**
 * @remarks This lemma is necessary to prove `$"Induction principle"` because structural induction is only allowed on theorem parameters.
 */
$"Induction lemma"(
  M: %Subset($"Natural numbers"),
  _1: %Constraint($inductive(M = M)),
  n: %Element($"Natural numbers")
): %StandardTheorem {
  claim = %in(n, M),
  proofs = [{steps = #(_: %ProveByInduction(
    n, $"Natural numbers",
    [
      {
        constructor = $"Natural numbers".zero,
        value = {steps = #(
          _: %Consider(_1),
          _: %UseDef(result = %in(
            $number(value = 0),
            M
          ))
        )},
        rewrite = %true
      },
      {
        constructor = $"Natural numbers".successor,
        parameters = #(x: %Element($"Natural numbers")),
        value = {steps = #(
          _: %UseTheorem(
            $"Induction lemma"(M = M, n = x),
            result = %in(x, M)
          ),
          _: %Consider(_1),
          _: %UseDef(result = %forall(
            #(m: %Element(M)),
            %in(
              $sum(
                m = m,
                n = $number(value = 1)
              ),
              M
            )
          )),
          _: %UseForAll({m = x})
        )},
        rewrite = %true
      }
    ]
  ))}]
}
