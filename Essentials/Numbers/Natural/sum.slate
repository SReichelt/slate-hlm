%../../../../../logics/hlm%

/**
 * @remarks Such recursive definitions are possible due to the definition of natural numbers as a construction (i.e. inductive data type). Note that some of the "+" symbols appearing in this definition are part of the notation of the successor constructor.
 *
 * @references * https://en.wikipedia.org/wiki/Natural_number#Addition
 *             * https://proofwiki.org/wiki/Definition:Addition/Natural_Numbers
 *             * https://coq.inria.fr/library/Coq.Init.Nat.html#add
 *             * https://coq.inria.fr/library/Coq.NArith.BinNatDef.html#N.add
 *             * https://leanprover-community.github.io/mathlib_docs/core/init/core.html#nat.add
 */
$sum(
  m: %Element($"Natural numbers"),
  n: %Element(%previous)
): %ExplicitOperator {
  notation = [$AdditionOperator(operands = [m, n])],
  definition = [%structuralCases(
    n,
    $"Natural numbers",
    [
      {
        constructor = $"Natural numbers".zero,
        value = m
      },
      {
        constructor = $"Natural numbers".successor,
        parameters = #(x: %Element($"Natural numbers")),
        value = $"Natural numbers".successor(n = {$sum(
          m = {m},
          n = {x}
        )})
      }
    ]
  )]
}
