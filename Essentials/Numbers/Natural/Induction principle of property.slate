%../../../../../logics/hlm%

[
  $~"has property" = $../../Properties/"has property"
]

/**
 * @remarks This is a formulation of the induction principle based on the definition of a property (see `$../../Properties/Properties`). In HLM, it is provable from structural induction.
 *
 * @references * https://en.wikipedia.org/wiki/Mathematical_induction
 *             * https://mathworld.wolfram.com/PrincipleofMathematicalInduction.html
 *             * https://proofwiki.org/wiki/Principle_of_Mathematical_Induction/Zero-Based
 */
$"Induction principle of property"(
  p: %Element($../../Properties/Properties(S = $"Natural numbers")),
  _1: %Constraint($~"has property"(
    S = $"Natural numbers", p = p,
    s = $number(value = 0)
  )),
  _2: %Constraint(%forall(
    #(
      m: %Element($"Natural numbers"),
      _3: %Constraint($~"has property"(S = $"Natural numbers", p = p, s = m))
    ),
    $~"has property"(
      S = $"Natural numbers", p = p,
      s = $sum(
        m = m,
        n = $number(value = 1)
      )
    )
  )),
  n: %Element($"Natural numbers")
): %StandardTheorem {
  claim = $~"has property"(S = $"Natural numbers", p = p, s = n),
  proofs = [{steps = #(_: %ProveByInduction(
    n, $"Natural numbers",
    [
      {
        constructor = $"Natural numbers".zero,
        value = {steps = #()},
        rewrite = %true
      },
      {
        constructor = $"Natural numbers".successor,
        parameters = #(x: %Element($"Natural numbers")),
        value = {steps = #(
          _: %UseTheorem(
            $"Induction principle of property"(p = p, n = x),
            result = $~"has property"(S = $"Natural numbers", p = p, s = x)
          ),
          _: %Consider(_2),
          _: %UseForAll({m = x})
        )},
        rewrite = %true
      }
    ]
  ))}]
}
