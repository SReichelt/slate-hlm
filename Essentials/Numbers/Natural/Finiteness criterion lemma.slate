%../../../../../logics/hlm%

[
  $~Functions = $../../Functions,
  $~Image = $../../Functions/Image,
  $~Relations = $../../Relations/Relations,
  $~"Segment (less or equal)" = $Subsets/"Segment (less or equal)",
  $~"greatest element" = $../../Relations/"greatest element",
  $~value = $../../Functions/value
]

/**
 * @remarks This lemma is necessary to prove the following theorem. `k` and `f` do not appear in the result, but they enable a proof via induction on `k`.
 */
$"Finiteness criterion lemma"(
  M: %Subset($"Natural numbers"),
  k: %Element($"Natural numbers"),
  f: %Element($~Functions/Bijections(
    X = $~"Segment (less or equal)"(n = k),
    Y = M
  ))
): %StandardTheorem {
  claim = %exists(
    #(m: %Element(M)),
    formula = $~"greatest element"(
      S = $"Natural numbers",
      ⪯ = $~Relations(S = $"Natural numbers", T = $"Natural numbers").relation(_1 = {
          #(m,n: %Element($"Natural numbers")),
          {p = $"less or equal"(m = m, n = n)}
      }),
      T = M, t = m
    )
  ),
  proofs = [{steps = #(_: %ProveByInduction(
    k, $"Natural numbers",
    [
      {
        constructor = $"Natural numbers".zero,
        value = {
          parameters = #(_: %Constraint(%equals(
            k,
            $number(value = 0)
          ))),
          steps = #(_: %ProveExists({m = $~value(
            X = $~"Segment (less or equal)"(n = k),
            Y = M, f = f,
            x = $number(value = 0)
          )}))
        },
        rewrite = %true
      },
      {
        constructor = $"Natural numbers".successor,
        parameters = #(x: %Element($"Natural numbers")),
        value = {
          parameters = #(_: %Constraint(%equals(
            k,
            $sum(
              m = x,
              n = $number(value = 1)
            )
          ))),
          steps = #(
            N: %SetDef($~Image(
              X = $~"Segment (less or equal)"(n = k),
              Y = M, f = f,
              S = $~"Segment (less or equal)"(n = x)
            )),
            _: %UseTheorem(
              $"Finiteness criterion lemma"(
                M = N, k = x,
                f = $~Functions/"codomain restriction"(
                  X = $~"Segment (less or equal)"(n = k),
                  Y = M, f = f,
                  A = $~"Segment (less or equal)"(n = x),
                  B = $~Image(
                    X = $~"Segment (less or equal)"(n = k),
                    Y = M, f = f,
                    S = $~"Segment (less or equal)"(n = x)
                  )
                )
              ),
              result = %exists(
                #(n: %Element(N)),
                formula = $~"greatest element"(
                  S = $"Natural numbers",
                  ⪯ = $~Relations(S = $"Natural numbers", T = $"Natural numbers").relation(_1 = {
                      #(m,n: %Element($"Natural numbers")),
                      {p = $"less or equal"(m = m, n = n)}
                  }),
                  T = N, t = n
                )
              )
            ),
            _: %UseExists(#(n: %Element(N))),
            _: %ProveExists({m = %cases([
              {
                formula = $less(
                  n = $~value(
                    X = $~"Segment (less or equal)"(n = k),
                    Y = M, f = f, x = k
                  ),
                  m = n
                ),
                value = $~value(
                  X = $~"Segment (less or equal)"(n = k),
                  Y = M, f = f, x = k
                )
              },
              {
                formula = $"less or equal"(
                  m = $~value(
                    X = $~"Segment (less or equal)"(n = k),
                    Y = M, f = f, x = k
                  ),
                  n = n
                ),
                value = n
              }
            ])})
          )
        },
        rewrite = %true
      }
    ]
  ))}]
}
