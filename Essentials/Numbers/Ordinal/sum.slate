%../../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Ordinal_arithmetic#Addition
 *             * http://mathworld.wolfram.com/OrdinalAddition.html
 *             * https://proofwiki.org/wiki/Definition:Ordinal_Addition
 *             * https://ncatlab.org/nlab/show/ordinal+sum
 *             * https://leanprover-community.github.io/mathlib_docs/set_theory/ordinal.html#ordinal.has_add
 */
$sum(
  α: %Element($"Ordinal numbers"),
  β: %Element(%previous)
): %ExplicitOperator {
  display = [$Operator(
    symbol = '+',
    operands = [α, β],
    minParenLevel = 1,
    maxParenLevel = 1
  )],
  definition = [%structuralCases(
    β,
    $"Ordinal numbers",
    [{
      constructor = $"Ordinal numbers"."order type",
      parameters = #(
        B: %Set,
        ⊑: %Element($../../Relations/Endorelations(S = {B})),
        _1: %Constraint($../../Relations/"well-order"(
          S = {B},
          ⪯ = {⊑}
        ))
      ),
      value = %structuralCases(
        α,
        $"Ordinal numbers",
        [{
          constructor = $"Ordinal numbers"."order type",
          parameters = #(
            A: %Set,
            ⪯: %Element($../../Relations/Endorelations(S = {A})),
            _1: %Constraint($../../Relations/"well-order"(
              S = {A},
              ⪯ = {⪯}
            ))
          ),
          value = $"Ordinal numbers"."order type"(
            S = {$../../Sets/"Disjoint union"(
              S = {A},
              T = {B}
            )},
            ⪯ = {$../../Relations/succession(
              S = {A},
              T = {B},
              ⪯ = {⪯},
              ⊑ = {⊑}
            )}
          )
        }]
      )
    }]
  )]
}
