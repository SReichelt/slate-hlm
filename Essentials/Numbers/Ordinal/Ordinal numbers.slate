%../../../../../logics/hlm%

[
  $~Endorelations = $../../Relations/Endorelations,
  $~"Natural numbers" = $../Natural/"Natural numbers",
  $~"Segment (less)" = $../Natural/Subsets/"Segment (less)",
  $~"well-order" = $../../Relations/"well-order"
]

/**
 * @references * https://en.wikipedia.org/wiki/Ordinal_number
 *             * https://mathworld.wolfram.com/OrdinalNumber.html
 *             * https://proofwiki.org/wiki/Definition:Ordinal
 *             * https://ncatlab.org/nlab/show/ordinal+number
 *             * https://leanprover-community.github.io/mathlib_docs/set_theory/ordinal.html#ordinal
 */
$"Ordinal numbers": %Construction {
  $"order type"(
    S: %Set,
    ⪯: %Element($~Endorelations(S = S)),
    _1: %Constraint($~"well-order"(S = S, ⪯ = ⪯))
  ): %Constructor {
    notation = $Tuple(
      items = [S, ⪯],
      style = '[]'
    ),
    equalityDefinition = {
      leftParameters = #(
        S: %Set,
        ⪯: %Element($~Endorelations(S = S)),
        _1: %Constraint($~"well-order"(S = S, ⪯ = ⪯))
      ),
      rightParameters = #(
        T: %Set,
        ⊑: %Element($~Endorelations(S = T)),
        _1: %Constraint($~"well-order"(S = T, ⪯ = ⊑))
      ),
      definition = [%exists(
        #(φ: %Element($../../Functions/Bijections(X = S, Y = T))),
        formula = $../../../Algebra/Generic/Isomorphisms/"relation condition"(X = S, Y = T, φ = φ, ≺ = ⪯, ⊂ = ⊑)
      )],
      isomorphic = %true
    }
  }

  notation = '𝓞𝓻𝓭',
  definitionNotation = {
    parameter = #(α: %Expr),
    singularName = 'ordinal number',
    pluralName = 'ordinal numbers',
    nameOptional = %true
  },
  embedding = {
    parameter = #(n: %Element($~"Natural numbers")),
    target = $"Ordinal numbers"."order type"(
      S = $~"Segment (less)"(n = n),
      ⪯ = $../../Relations/restriction(
        S = $~"Natural numbers",
        T = $~"Segment (less)"(n = n),
        ≺ = $../../Relations/Relations(S = $~"Natural numbers", T = $~"Natural numbers").relation(_1 = {
            #(m,n: %Element($~"Natural numbers")),
            {p = $../Natural/"less or equal"(m = m, n = n)}
        })
      )
    )
  },
  rewrite = {
    parameter = #(α: %Element($"Ordinal numbers")),
    value = $"Ordinal numbers"."order type"(
      S = $Carrier(α = α),
      ⪯ = $~Endorelations(S = $Carrier(α = α)).relation(_1 = {
          #(a,b: %Element($Carrier(α = α))),
          {p = $before(α = α, a = a, b = b)}
      })
    )
  }
}
