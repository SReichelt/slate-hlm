%../../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Cardinal_number
 *             * https://leanprover-community.github.io/mathlib_docs/set_theory/cardinal.html#cardinal.has_le
 */
$"less or equal"(
  κ: %Element($"Cardinal numbers"),
  μ: %Element(%previous)
): %Predicate {
  notation = [
    $Relation(
      symbol = %neg('≤', '≰'),
      operands = [κ, μ]
    ),
    $Relation(
      symbol = %neg('≥', '≱'),
      operands = [μ, κ]
    )
  ],
  definition = [
    %exists(
      #(φ: %Element($Homomorphisms/Homomorphisms(
        κ = {κ},
        μ = {μ}
      ))),
      formula = $../../Functions/injective(
        X = {$Carrier(κ = {κ})},
        Y = {$Carrier(κ = {μ})},
        f = {φ}
      )
    ),
    %exists(
      #(S: %Subset($Carrier(κ = {μ}))),
      formula = %equals(
        $"Cardinal numbers".cardinality(S = {S}),
        κ
      )
    )
  ]
}
