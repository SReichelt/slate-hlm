%../../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Real_number
 *             * http://mathworld.wolfram.com/RealNumber.html
 *             * https://proofwiki.org/wiki/Definition:Real_Number
 *             * https://ncatlab.org/nlab/show/real+number
 *             * https://coq.inria.fr/library/Coq.Reals.ConstructiveCauchyReals.html#CReal
 *             * https://leanprover-community.github.io/mathlib_docs/data/real/basic.html#real
 */
$"Real numbers": %Construction {
  $class(
    r: %Element($../../Sequences/"Infinite sequences"(S = {$../Rational/"Rational numbers"})),
    _1: %Constraint($../Rational/Sequences/cauchy(𝐚 = {r}))
  ): %Constructor {
    display = [$Parens(
      body = r,
      style = '[]'
    )],
    equalityDefinition = {
      leftParameters = #(
        r: %Element($../../Sequences/"Infinite sequences"(S = {$../Rational/"Rational numbers"})),
        _1: %Constraint($../Rational/Sequences/cauchy(𝐚 = {r}))
      ),
      rightParameters = #(
        "r'": %Element($../../Sequences/"Infinite sequences"(S = {$../Rational/"Rational numbers"})),
        _2: %Constraint($../Rational/Sequences/cauchy(𝐚 = {"r'"}))
      ),
      definition = [$../Rational/Sequences/converges(
        𝐚 = {$../Rational/Sequences/"sequence difference"(
          𝐛 = {r},
          𝐚 = {"r'"}
        )},
        l = {$../Natural/number(value = 0)}
      )]
    }
  }

  display = ['ℝ'],
  embedding = {
    parameter = #(x: %Element($../Rational/"Rational numbers")),
    target = $"Real numbers".class(r = {$../../Sequences/"Infinite sequences"(S = {$../Rational/"Rational numbers"}).sequence(n = {
        #(k: %Element($../Natural/"Natural numbers")),
        {a = {x}}
    })})
  }
}
