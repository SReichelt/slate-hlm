%../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Injective_function
 *             * https://mathworld.wolfram.com/Injection.html
 *             * https://proofwiki.org/wiki/Definition:Injection
 *             * https://ncatlab.org/nlab/show/injection
 *             * https://leanprover-community.github.io/mathlib_docs/core/init/function.html#function.injective
 */
$injective(
  X,Y: %Set(auto = %true),
  f: %Element($Functions(X = X, Y = Y))
): %Predicate {
  notation = $Property(operand = f, property = 'injective'),
  definition = [
    %forall(
      #(
        a,b: %Element(X),
        _1: %Constraint(%equals(
          $value(X = X, Y = Y, f = f, x = a),
          $value(X = X, Y = Y, f = f, x = b)
        ))
      ),
      %equals(a, b)
    ),
    %forall(
      #(
        c: %Element(Y),
        d,e: %Element($Preimage(
          X = X,
          Y = Y,
          f = f,
          S = %enumeration(c)
        ))
      ),
      %equals(d, e)
    ),
    %forall(
      #(y: %Element($Image(X = X, Y = Y, f = f, S = X))),
      %existsUnique(
        #(x: %Element(X)),
        formula = %equals(
          $value(X = X, Y = Y, f = f, x = x),
          y
        )
      )
    ),
    %or(
      $../Sets/empty(S = X),
      %exists(
        #(g: %Element($Functions(X = Y, Y = X))),
        formula = %equals(
          $composition(X = X, Y = Y, Z = X, f = f, g = g),
          $identity(X = X)
        )
      )
    )
  ]
}
