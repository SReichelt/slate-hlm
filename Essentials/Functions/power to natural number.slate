%../../../../logics/hlm%

[
  $~"Natural numbers" = $../Numbers/Natural/"Natural numbers"
]

/**
 * @references * https://en.wikipedia.org/wiki/Iterated_function
 *             * https://mathworld.wolfram.com/IteratedMap.html
 */
$"power to natural number"(
  X: %Set(auto = %true),
  f: %Element($Functions(X = X, Y = X)),
  n: %Element($~"Natural numbers")
): %ExplicitOperator {
  notation = $SubSup(body = f, sup = n),
  definition = [%structuralCases(
    n, $~"Natural numbers",
    [
      {
        constructor = $~"Natural numbers".zero,
        value = $identity(X = X),
        rewrite = %true
      },
      {
        constructor = $~"Natural numbers".successor,
        parameters = #(x: %Element($~"Natural numbers")),
        value = $composition(
          X = X, Y = X, Z = X,
          f = $"power to natural number"(X = X, f = f, n = x),
          g = f
        ),
        rewrite = %true
      }
    ]
  )]
}
