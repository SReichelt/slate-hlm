%../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Ordinal_number#Transfinite_sequence (but we use arbitrary ordinals, not only limit ordinals)
 */
$"Ordinal-indexed sequences"(S: %Set(auto = %true)): %Construction {
  $sequence(
    α: %Element($../Numbers/Ordinal/"Ordinal numbers"),
    𝐚: %Element($"Fixed-length sequences"(
      S = {S},
      α = {α}
    ))
  ): %Constructor {
    display = [$Tuple(items = [α, 𝐚])],
    equalityDefinition = {
      leftParameters = #(
        α: %Element($../Numbers/Ordinal/"Ordinal numbers"),
        𝐚: %Element($"Fixed-length sequences"(
          S = {S},
          α = {α}
        ))
      ),
      rightParameters = #(
        β: %Element($../Numbers/Ordinal/"Ordinal numbers"),
        𝐛: %Element($"Fixed-length sequences"(
          S = {S},
          α = {β}
        ))
      ),
      definition = [%and(
        %equals(α, β),
        %equals(
          𝐚,
          %asElementOf(
            𝐛,
            $"Fixed-length sequences"(
              S = {S},
              α = {α}
            )
          )
        )
      )],
      isomorphic = %true
    }
  }

  display = [$SubSup(
    body = S,
    sup = '⋆'
  )],
  definitionDisplay = {
    parameter = #(𝒂: %Expr),
    singularName = ['ordinal-indexed sequence', ' on ', S],
    pluralName = ['ordinal-indexed sequences', ' on ', S]
  },
  embedding = {
    parameter = #(𝒂: %Element($"Finite sequences"(S = {S}))),
    target = $"Ordinal-indexed sequences".sequence(
      α = {$"finite sequence length"(
        S = {S},
        𝒂 = {𝒂}
      )},
      𝐚 = {%structuralCases(
        𝒂,
        $"Finite sequences"(S = {S}),
        [{
          constructor = $"Finite sequences".sequence,
          parameters = #(
            n: %Element($../Numbers/Natural/"Natural numbers"),
            𝐚: %Element($"Fixed-length sequences"(
              S = {S},
              α = {n}
            ))
          ),
          value = %asElementOf(
            𝐚,
            $"Fixed-length sequences"(
              S = {S},
              α = {$"finite sequence length"(
                S = {S},
                𝒂 = {𝒂}
              )}
            )
          )
        }]
      )}
    )
  }
}
