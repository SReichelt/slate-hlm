%../../../../logics/hlm%

/**
 * @remarks Finite sequences could alternatively be defined inductively (i.e. as lists). However, the given definition is closer to the definition of [infinite sequences](Infinite%20sequences), and often more useful in mathematical contexts.
 *
 * @references * https://en.wikipedia.org/wiki/Sequence
 *             * http://mathworld.wolfram.com/Sequence.html
 *             * https://proofwiki.org/wiki/Definition:Finite_Sequence
 *             * https://ncatlab.org/nlab/show/free+monoid
 *             * https://coq.inria.fr/library/Coq.Lists.List.html
 */
$"Finite sequences"(S: %Set(auto = %true)): %Construction {
  $sequence(
    n: %Element($../Numbers/Natural/"Natural numbers"),
    𝐚: %Element($Tuples(
      S = {S},
      α = {n}
    ))
  ): %Constructor {
    display = [$SubSup(
      body = $Parens(
        body = 𝐚,
        style = '()'
      ),
      sub = $SubSup(
        body = S,
        sup = n
      )
    )],
    equalityDefinition = {
      leftParameters = #(
        m: %Element($../Numbers/Natural/"Natural numbers"),
        𝐚: %Element($Tuples(
          S = {S},
          α = {m}
        ))
      ),
      rightParameters = #(
        n: %Element($../Numbers/Natural/"Natural numbers"),
        𝐛: %Element($Tuples(
          S = {S},
          α = {n}
        ))
      ),
      definition = [%and(
        %equals(m, n),
        %equals(
          𝐚,
          %asElementOf(
            𝐛,
            $Tuples(
              S = {S},
              α = {m}
            )
          )
        )
      )],
      isomorphic = %true
    }
  }

  display = [$SubSup(
    body = S,
    sup = '∗'
  )],
  definitionDisplay = {
    parameter = #(𝒂: %Expr),
    singularName = ['finite sequence', ' on ', S],
    pluralName = ['finite sequences', ' on ', S]
  }
}
