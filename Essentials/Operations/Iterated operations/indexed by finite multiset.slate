%../../../../../logics/hlm%

$"indexed by finite multiset"(
  X: %Set(auto = %true),
  ⨂: %Element($"Iterated operations"(X = X)),
  S: %Set(auto = %true),
  𝑰: %Element($../../Sets/Multisets/Multisets(S = S)),
  _1: %Constraint($../../Sets/Multisets/finite(S = S, 𝑻 = 𝑰)),
  _2: %Binder(
    #(i: %Element($../../Sets/Multisets/Set(S = S, 𝑻 = 𝑰))),
    #(a: %Element(X))
  )
): %ExplicitOperator {
  notation = $LargeOperator(
    symbol = ⨂,
    operand = a,
    under = $ElementRelation(operands = [i, 𝑰]),
    maxParenLevel = 1
  ),
  definition = [
    $"indexed by finite set"(
      X = X,
      ⨂ = ⨂,
      I = $../../Sets/Multisets/Elements(S = S, 𝑻 = 𝑰),
      _2 = {
        #(j: %Element($../../Sets/Multisets/Elements(S = S, 𝑻 = 𝑰))),
        {a = %structuralCases(
          j,
          $../../Sets/"Cartesian product"(
            S = S,
            T = $../../Numbers/Natural/"Natural numbers"
          ),
          [{
            constructor = $../../Sets/"Cartesian product".pair,
            parameters = #(
              t: %Element(S),
              n: %Element($../../Numbers/Natural/"Natural numbers")
            ),
            value = a[i = t]
          }]
        )}
      }
    ),
    $"indexed by finite set"(
      X = X,
      ⨂ = ⨂,
      I = $../../Sets/Multisets/Set(S = S, 𝑻 = 𝑰),
      _2 = {
        #(j: %Element($../../Sets/Multisets/Set(S = S, 𝑻 = 𝑰))),
        {a = $"indexed by natural number"(
          X = X,
          ⨂ = ⨂,
          n = $../../Sets/Multisets/multiplicity(S = S, 𝑻 = 𝑰, t = j),
          _1 = {
            #(k: %Element($../../Numbers/Natural/Subsets/"Segment (less)"(n = $../../Sets/Multisets/multiplicity(S = S, 𝑻 = 𝑰, t = j)))),
            {a = a[i = j]}
          }
        )}
      }
    ),
    $"indexed by set with finite support"(
      X = X,
      ⨂ = ⨂,
      I = S,
      _1 = {
        #(j: %Element(S)),
        {a = $"indexed by natural number"(
          X = X,
          ⨂ = ⨂,
          n = $../../Sets/Multisets/multiplicity(S = S, 𝑻 = 𝑰, t = j),
          _1 = {
            #(k: %Element($../../Numbers/Natural/Subsets/"Segment (less)"(n = $../../Sets/Multisets/multiplicity(S = S, 𝑻 = 𝑰, t = j)))),
            {a = a[i = j]}
          }
        )}
      }
    )
  ]
}
