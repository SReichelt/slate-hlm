%../../../../logics/hlm%

/**
 * @remarks The objects of this category are actually isomorphism classes of sets, i.e. cardinal numbers. In HLM, arbitrary sets cannot be used as objects because it is impossible to ask whether two arbitrary sets are equal. However, cardinal numbers work fine as a replacement. The situation with other categories is similar.
 *
 * @references * https://en.wikipedia.org/wiki/Category_of_sets
 *             * https://ncatlab.org/nlab/show/Set
 */
$cardinals(u: %Element($../../Essentials/Numbers/Natural/"Natural numbers")): %ExplicitOperator {
  display = [$SubSup(
    body = '𝐒𝐞𝐭',
    sub = u
  )],
  definition = [$../Categories(u = {u}).category(
      O = {$../../Essentials/Numbers/Cardinal/"Cardinal numbers"},
      𝐀 = {
        #(𝐀: %Element($../../Essentials/Numbers/Cardinal/"Cardinal numbers")),
        {𝐁 = {
          #(𝐁: %Element($../../Essentials/Numbers/Cardinal/"Cardinal numbers")),
          {M = {$../../Essentials/Numbers/Cardinal/Homomorphisms/Homomorphisms(
            𝐱 = {𝐀},
            𝐲 = {𝐁}
          )}}
        }}
      },
      𝐂 = {
        #(𝐂: %Element($../../Essentials/Numbers/Cardinal/"Cardinal numbers")),
        {i = {$../../Essentials/Numbers/Cardinal/Homomorphisms/identity(𝐱 = {𝐂})}}
      },
      𝐃 = {
        #(𝐃: %Element($../../Essentials/Numbers/Cardinal/"Cardinal numbers")),
        {𝐄 = {
          #(𝐄: %Element($../../Essentials/Numbers/Cardinal/"Cardinal numbers")),
          {𝐅 = {
            #(𝐅: %Element($../../Essentials/Numbers/Cardinal/"Cardinal numbers")),
            {∘ = {$../../Essentials/Functions/Operations/operator(
              X = {$../../Essentials/Numbers/Cardinal/Homomorphisms/Homomorphisms(
                𝐱 = {𝐄},
                𝐲 = {𝐅}
              )},
              Y = {$../../Essentials/Numbers/Cardinal/Homomorphisms/Homomorphisms(
                𝐱 = {𝐃},
                𝐲 = {𝐄}
              )},
              Z = {$../../Essentials/Numbers/Cardinal/Homomorphisms/Homomorphisms(
                𝐱 = {𝐃},
                𝐲 = {𝐅}
              )},
              parameters = #(
                ψ: %Element($../../Essentials/Numbers/Cardinal/Homomorphisms/Homomorphisms(
                  𝐱 = {𝐄},
                  𝐲 = {𝐅}
                )),
                φ: %Element($../../Essentials/Numbers/Cardinal/Homomorphisms/Homomorphisms(
                  𝐱 = {𝐃},
                  𝐲 = {𝐄}
                ))
              ),
              operator = $../../Essentials/Functions/composition(
                X = {$../../Essentials/Numbers/Cardinal/Carrier(𝐱 = {𝐃})},
                Y = {$../../Essentials/Numbers/Cardinal/Carrier(𝐱 = {𝐄})},
                Z = {$../../Essentials/Numbers/Cardinal/Carrier(𝐱 = {𝐅})},
                f = {φ},
                g = {ψ}
              )
            )}}
          }}
        }}
      }
  )]
}
