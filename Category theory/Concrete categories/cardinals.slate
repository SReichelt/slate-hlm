%../../../../logics/hlm%

/**
 * @remarks The objects of this category are actually isomorphism classes of sets, i.e. cardinal numbers. In HLM, arbitrary sets cannot be used as objects because it is impossible to ask whether two arbitrary sets are equal. However, cardinal numbers work fine as a replacement, due to the ability to obtain a representative set from a cardinal number. The situation with other categories is similar.
 *
 * @references * https://en.wikipedia.org/wiki/Category_of_sets
 *             * https://proofwiki.org/wiki/Definition:Category_of_Sets
 *             * https://ncatlab.org/nlab/show/Set
 *             * https://leanprover-community.github.io/mathlib_docs/category_theory/types.html#category_theory.types
 */
$cardinals(u: %Element($../../Essentials/Numbers/Natural/"Natural numbers")): %ExplicitOperator {
  display = [$SubSup(
    body = '𝐒𝐞𝐭',
    sub = u
  )],
  definition = [$../Categories(u = {u}).category(
      O = {$../../Essentials/Numbers/Cardinal/"Cardinal numbers"},
      𝐀 = {
        #(𝐀: %Element($../../Essentials/Numbers/Cardinal/"Cardinal numbers")),
        {𝐁 = {
          #(𝐁: %Element($../../Essentials/Numbers/Cardinal/"Cardinal numbers")),
          {M = {$../../Essentials/Numbers/Cardinal/Homomorphisms/Homomorphisms(
            κ = {𝐀},
            μ = {𝐁}
          )}}
        }}
      },
      𝐂 = {
        #(𝐂: %Element($../../Essentials/Numbers/Cardinal/"Cardinal numbers")),
        {i = {$../../Essentials/Numbers/Cardinal/Homomorphisms/identity(κ = {𝐂})}}
      },
      𝐃 = {
        #(𝐃: %Element($../../Essentials/Numbers/Cardinal/"Cardinal numbers")),
        {𝐄 = {
          #(𝐄: %Element($../../Essentials/Numbers/Cardinal/"Cardinal numbers")),
          {𝐅 = {
            #(𝐅: %Element($../../Essentials/Numbers/Cardinal/"Cardinal numbers")),
            {∘ = {$../../Essentials/Functions/Operations/operator(
              X = {$../../Essentials/Numbers/Cardinal/Homomorphisms/Homomorphisms(
                κ = {𝐄},
                μ = {𝐅}
              )},
              Y = {$../../Essentials/Numbers/Cardinal/Homomorphisms/Homomorphisms(
                κ = {𝐃},
                μ = {𝐄}
              )},
              Z = {$../../Essentials/Numbers/Cardinal/Homomorphisms/Homomorphisms(
                κ = {𝐃},
                μ = {𝐅}
              )},
              parameters = #(
                ψ: %Element($../../Essentials/Numbers/Cardinal/Homomorphisms/Homomorphisms(
                  κ = {𝐄},
                  μ = {𝐅}
                )),
                φ: %Element($../../Essentials/Numbers/Cardinal/Homomorphisms/Homomorphisms(
                  κ = {𝐃},
                  μ = {𝐄}
                ))
              ),
              operator = $../../Essentials/Functions/composition(
                X = {$../../Essentials/Numbers/Cardinal/Carrier(κ = {𝐃})},
                Y = {$../../Essentials/Numbers/Cardinal/Carrier(κ = {𝐄})},
                Z = {$../../Essentials/Numbers/Cardinal/Carrier(κ = {𝐅})},
                f = {φ},
                g = {ψ}
              )
            )}}
          }}
        }}
      }
  )]
}
