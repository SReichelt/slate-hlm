%../../../../logics/hlm%

/**
 * @remarks This defines the category of small categories, or, more generally, a category in `$../../Essentials/Numbers/Natural/sum(m = {u}, n = {$../../Essentials/Numbers/Natural/number(value = 1)})` of all categories in `u`.
 *
 * @references * https://en.wikipedia.org/wiki/Category_of_small_categories
 *             * https://proofwiki.org/wiki/Definition:Category_of_Categories
 *             * https://ncatlab.org/nlab/show/Cat
 *             * https://leanprover-community.github.io/mathlib_docs/category_theory/category/Cat.html#category_theory.Cat
 */
$categories(u: %Element($../../Essentials/Numbers/Natural/"Natural numbers")): %ExplicitOperator {
  notation = $Function(
    function = '𝐂𝐚𝐭',
    arguments = [u]
  ),
  definition = [$../Categories(u = {$../../Essentials/Numbers/Natural/sum(
      m = {u},
      n = {$../../Essentials/Numbers/Natural/number(value = 1)}
    )}).category(
      O = {$../Categories(u = {u})},
      _1 = {
        #(𝓐,𝓑: %Element($../Categories(u = {u}))),
        {M = {$../Functors/Functors(
          u = {u},
          v = {u},
          𝓒 = {𝓐},
          𝓓 = {𝓑}
        )}}
      },
      _2 = {
        #(𝓒: %Element($../Categories(u = {u}))),
        {i = {$../Functors/identity(
          u = {u},
          𝓒 = {𝓒}
        )}}
      },
      _3 = {
        #(𝓓,𝓔,𝓕: %Element($../Categories(u = {u}))),
        {∘ = {$../../Essentials/Operations/Operations(
            X = {$../Functors/Functors(
              u = {u},
              v = {u},
              𝓒 = {𝓔},
              𝓓 = {𝓕}
            )},
            Y = {$../Functors/Functors(
              u = {u},
              v = {u},
              𝓒 = {𝓓},
              𝓓 = {𝓔}
            )},
            Z = {$../Functors/Functors(
              u = {u},
              v = {u},
              𝓒 = {𝓓},
              𝓓 = {𝓕}
            )}
          ).operation(_1 = {
            #(
              G: %Element($../Functors/Functors(
                u = {u},
                v = {u},
                𝓒 = {𝓔},
                𝓓 = {𝓕}
              )),
              F: %Element($../Functors/Functors(
                u = {u},
                v = {u},
                𝓒 = {𝓓},
                𝓓 = {𝓔}
              ))
            ),
            {z = {$../Functors/composition(
              u = {u},
              v = {u},
              w = {u},
              𝓒 = {𝓓},
              𝓓 = {𝓔},
              𝓔 = {𝓕},
              F = {F},
              G = {G}
            )}}
        })}}
      }
  )]
}
