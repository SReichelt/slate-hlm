%../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Functor
 *             * http://mathworld.wolfram.com/Functor.html
 *             * https://proofwiki.org/wiki/Definition:Functor
 *             * https://ncatlab.org/nlab/show/functor
 */
$Functors(
  u: %Element(
    $../../Essentials/Numbers/Natural/"Natural numbers",
    auto = %true
  ),
  v: %Element(
    %previous,
    auto = %true
  ),
  𝓒: %Element($../Categories(u = {u})),
  𝓓: %Element($../Categories(u = {v}))
): %Construction {
  $functor(
    𝐗: %Binding(
      $../Objects(
        u = {u},
        𝓒 = {𝓒}
      ),
      #(𝐃: %Element($../Objects(
        u = {v},
        𝓒 = {𝓓}
      )))
    ),
    𝐘: %Binding(
      $../Objects(
        u = {u},
        𝓒 = {𝓒}
      ),
      #(𝐙: %Binding(
        %previous,
        #(f: %Binding(
          $../Morphisms(
            u = {u},
            𝓒 = {𝓒},
            𝐗 = {𝐘},
            𝐘 = {𝐙}
          ),
          #(d: %Element($../Morphisms(
            u = {v},
            𝓒 = {𝓓},
            𝐗 = {𝐃[𝐘]},
            𝐘 = {𝐃[𝐙]}
          )))
        ))
      ))
    ),
    _1: %Constraint(%forall(
      #(𝐗: %Element($../Objects(
        u = {u},
        𝓒 = {𝓒}
      ))),
      %equals(
        d[
          𝐗,
          𝐗,
          $../identity(
            u = {u},
            𝓒 = {𝓒},
            𝐗 = {𝐗}
          )
        ],
        $../identity(
          u = {v},
          𝓒 = {𝓓},
          𝐗 = {𝐃[𝐗]}
        )
      )
    )),
    _2: %Constraint(%forall(
      #(
        𝐗: %Element($../Objects(
          u = {u},
          𝓒 = {𝓒}
        )),
        𝐘: %Element(%previous),
        𝐙: %Element(%previous),
        f: %Element($../Morphisms(
          u = {u},
          𝓒 = {𝓒},
          𝐗 = {𝐗},
          𝐘 = {𝐘}
        )),
        g: %Element($../Morphisms(
          u = {u},
          𝓒 = {𝓒},
          𝐗 = {𝐘},
          𝐘 = {𝐙}
        ))
      ),
      %equals(
        d[
          𝐗,
          𝐙,
          $../composition(
            u = {u},
            𝓒 = {𝓒},
            𝐗 = {𝐗},
            𝐘 = {𝐘},
            𝐙 = {𝐙},
            g = {g},
            f = {f}
          )
        ],
        $../composition(
          u = {v},
          𝓒 = {𝓓},
          𝐗 = {𝐃[𝐗]},
          𝐘 = {𝐃[𝐘]},
          𝐙 = {𝐃[𝐙]},
          g = {d[𝐘,𝐙,g]},
          f = {d[𝐗,𝐘,f]}
        )
      )
    ))
  ): %Constructor {
    display = [$Parens(
      body = $Table(
        items = [
          [
            $InnerParens(
              body = 𝓒,
              maxLevel = 0
            ),
            ' → ',
            $InnerParens(
              body = 𝓓,
              maxLevel = 0
            )
          ],
          [𝐗, ' ↦ ', 𝐃],
          [
            $Parens(
              body = $FunctionDefinition(
                function = f,
                symbol = '→',
                operands = [𝐘, 𝐙]
              ),
              style = '()'
            ),
            ' ↦ ',
            d
          ]
        ],
        style = 'aligned'
      ),
      style = '()'
    )],
    definitionDisplay = {
      parameter = #(F: %Expr),
      display = [$FunctionPrefix(
        function = F,
        body = [
          ' ',
          $Table(
            items = [
              [
                $InnerParens(
                  body = 𝓒,
                  maxLevel = 0
                ),
                ' → ',
                $InnerParens(
                  body = 𝓓,
                  maxLevel = 0
                )
              ],
              [𝐗, ' ↦ ', 𝐃],
              [
                $Parens(
                  body = $FunctionDefinition(
                    function = f,
                    symbol = '→',
                    operands = [𝐘, 𝐙]
                  ),
                  style = '()'
                ),
                ' ↦ ',
                d
              ]
            ],
            style = 'aligned'
          )
        ]
      )]
    },
    equalityDefinition = {
      leftParameters = #(
        𝐗: %Binding(
          $../Objects(
            u = {u},
            𝓒 = {𝓒}
          ),
          #(𝐃: %Element($../Objects(
            u = {v},
            𝓒 = {𝓓}
          )))
        ),
        𝐘: %Binding(
          $../Objects(
            u = {u},
            𝓒 = {𝓒}
          ),
          #(𝐙: %Binding(
            %previous,
            #(f: %Binding(
              $../Morphisms(
                u = {u},
                𝓒 = {𝓒},
                𝐗 = {𝐘},
                𝐘 = {𝐙}
              ),
              #(d: %Element($../Morphisms(
                u = {v},
                𝓒 = {𝓓},
                𝐗 = {𝐃[𝐘]},
                𝐘 = {𝐃[𝐙]}
              )))
            ))
          ))
        ),
        _1: %Constraint(%forall(
          #(𝐗: %Element($../Objects(
            u = {u},
            𝓒 = {𝓒}
          ))),
          %equals(
            d[
              𝐗,
              𝐗,
              $../identity(
                u = {u},
                𝓒 = {𝓒},
                𝐗 = {𝐗}
              )
            ],
            $../identity(
              u = {v},
              𝓒 = {𝓓},
              𝐗 = {𝐃[𝐗]}
            )
          )
        )),
        _2: %Constraint(%forall(
          #(
            𝐗: %Element($../Objects(
              u = {u},
              𝓒 = {𝓒}
            )),
            𝐘: %Element(%previous),
            𝐙: %Element(%previous),
            f: %Element($../Morphisms(
              u = {u},
              𝓒 = {𝓒},
              𝐗 = {𝐗},
              𝐘 = {𝐘}
            )),
            g: %Element($../Morphisms(
              u = {u},
              𝓒 = {𝓒},
              𝐗 = {𝐘},
              𝐘 = {𝐙}
            ))
          ),
          %equals(
            d[
              𝐗,
              𝐙,
              $../composition(
                u = {u},
                𝓒 = {𝓒},
                𝐗 = {𝐗},
                𝐘 = {𝐘},
                𝐙 = {𝐙},
                g = {g},
                f = {f}
              )
            ],
            $../composition(
              u = {v},
              𝓒 = {𝓓},
              𝐗 = {𝐃[𝐗]},
              𝐘 = {𝐃[𝐘]},
              𝐙 = {𝐃[𝐙]},
              g = {d[𝐘,𝐙,g]},
              f = {d[𝐗,𝐘,f]}
            )
          )
        ))
      ),
      rightParameters = #(
        𝐗: %Binding(
          $../Objects(
            u = {u},
            𝓒 = {𝓒}
          ),
          #("𝐃'": %Element($../Objects(
            u = {v},
            𝓒 = {𝓓}
          )))
        ),
        𝐘: %Binding(
          $../Objects(
            u = {u},
            𝓒 = {𝓒}
          ),
          #(𝐙: %Binding(
            %previous,
            #(f: %Binding(
              $../Morphisms(
                u = {u},
                𝓒 = {𝓒},
                𝐗 = {𝐘},
                𝐘 = {𝐙}
              ),
              #("d'": %Element($../Morphisms(
                u = {v},
                𝓒 = {𝓓},
                𝐗 = {"𝐃'"[𝐘]},
                𝐘 = {"𝐃'"[𝐙]}
              )))
            ))
          ))
        ),
        _1: %Constraint(%forall(
          #(𝐗: %Element($../Objects(
            u = {u},
            𝓒 = {𝓒}
          ))),
          %equals(
            "d'"[
              𝐗,
              𝐗,
              $../identity(
                u = {u},
                𝓒 = {𝓒},
                𝐗 = {𝐗}
              )
            ],
            $../identity(
              u = {v},
              𝓒 = {𝓓},
              𝐗 = {"𝐃'"[𝐗]}
            )
          )
        )),
        _2: %Constraint(%forall(
          #(
            𝐗: %Element($../Objects(
              u = {u},
              𝓒 = {𝓒}
            )),
            𝐘: %Element(%previous),
            𝐙: %Element(%previous),
            f: %Element($../Morphisms(
              u = {u},
              𝓒 = {𝓒},
              𝐗 = {𝐗},
              𝐘 = {𝐘}
            )),
            g: %Element($../Morphisms(
              u = {u},
              𝓒 = {𝓒},
              𝐗 = {𝐘},
              𝐘 = {𝐙}
            ))
          ),
          %equals(
            "d'"[
              𝐗,
              𝐙,
              $../composition(
                u = {u},
                𝓒 = {𝓒},
                𝐗 = {𝐗},
                𝐘 = {𝐘},
                𝐙 = {𝐙},
                g = {g},
                f = {f}
              )
            ],
            $../composition(
              u = {v},
              𝓒 = {𝓓},
              𝐗 = {"𝐃'"[𝐗]},
              𝐘 = {"𝐃'"[𝐘]},
              𝐙 = {"𝐃'"[𝐙]},
              g = {"d'"[𝐘,𝐙,g]},
              f = {"d'"[𝐗,𝐘,f]}
            )
          )
        ))
      ),
      definition = [%and(
        %forall(
          #(𝐗: %Element($../Objects(
            u = {u},
            𝓒 = {𝓒}
          ))),
          %equals(𝐃[𝐗], "𝐃'"[𝐗])
        ),
        %forall(
          #(
            𝐘: %Element($../Objects(
              u = {u},
              𝓒 = {𝓒}
            )),
            𝐙: %Element(%previous),
            f: %Element($../Morphisms(
              u = {u},
              𝓒 = {𝓒},
              𝐗 = {𝐘},
              𝐘 = {𝐙}
            ))
          ),
          %equals(
            d[𝐘,𝐙,f],
            %asElementOf(
              "d'"[𝐘,𝐙,f],
              $../Morphisms(
                u = {v},
                𝓒 = {𝓓},
                𝐗 = {𝐃[𝐘]},
                𝐘 = {𝐃[𝐙]}
              )
            )
          )
        )
      )],
      isomorphic = %true
    }
  }

  display = [$Function(
    function = 'Fun',
    arguments = [𝓒, 𝓓]
  )],
  definitionDisplay = {
    parameter = #(F: %Expr),
    display = [$FunctionDefinition(
      function = F,
      symbol = '→',
      operands = [𝓒, 𝓓]
    )],
    singularName = 'functor',
    pluralName = 'functors',
    nameOptional = %true
  }
}
