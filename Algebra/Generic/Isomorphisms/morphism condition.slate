%../../../../../logics/hlm%

[
  $~Bijections = $../../../Essentials/Functions/Bijections,
  $~"Cartesian product" = $../../../Essentials/Sets/"Cartesian product",
  $~Functions = $../../../Essentials/Functions,
  $~Operations = $../../../Essentials/Operations/Operations,
  $~composition = $../../../Essentials/Functions/composition
]

$"morphism condition"(
  X_1,X_2,X_3,Y_1,Y_2,Y_3: %Set(auto = %true),
  φ_1: %Element($~Bijections(X = X_1, Y = Y_1)),
  φ_2: %Element($~Bijections(X = X_2, Y = Y_2)),
  φ_3: %Element($~Bijections(X = X_3, Y = Y_3)),
  ∗: %Element($~Operations(X = X_1, Y = X_2, Z = X_3)),
  ⋆: %Element($~Operations(X = Y_1, Y = Y_2, Z = Y_3))
): %Predicate {
  notation = $SpecificIsomorphismRelation(
    operands = [∗, ⋆],
    function = $Group(items = [φ_1, φ_2, φ_3])
  ),
  definition = [
    %structural(
      ⋆,
      $~Operations(X = Y_1, Y = Y_2, Z = Y_3),
      [{
        constructor = $~Operations.operation,
        parameters = #(_2: %Binder(
          #(
            y_1: %Element(Y_1),
            y_2: %Element(Y_2)
          ),
          #(b: %Element(Y_3))
        )),
        value = %structural(
          ∗,
          $~Operations(X = X_1, Y = X_2, Z = X_3),
          [{
            constructor = $~Operations.operation,
            parameters = #(_1: %Binder(
              #(
                x_1: %Element(X_1),
                x_2: %Element(X_2)
              ),
              #(a: %Element(X_3))
            )),
            value = %forall(
              #(
                x_1: %Element(X_1),
                x_2: %Element(X_2)
              ),
              %equals(
                $~Functions/value(
                  X = X_3, Y = Y_3, f = φ_3,
                  x = a[x_1 = x_1, x_2 = x_2]
                ),
                b[
                  y_1 = $~Functions/value(X = X_1, Y = Y_1, f = φ_1, x = x_1),
                  y_2 = $~Functions/value(X = X_2, Y = Y_2, f = φ_2, x = x_2)
                ]
              )
            )
          }]
        )
      }]
    ),
    %forall(
      #(
        x_1: %Element(X_1),
        x_2: %Element(X_2)
      ),
      %equals(
        $~Functions/value(
          X = X_3, Y = Y_3, f = φ_3,
          x = $../../../Essentials/Operations/value(X = X_1, Y = X_2, Z = X_3, ∗ = ∗, x = x_1, y = x_2)
        ),
        $../../../Essentials/Operations/value(
          X = Y_1, Y = Y_2, Z = Y_3, ∗ = ⋆,
          x = $~Functions/value(X = X_1, Y = Y_1, f = φ_1, x = x_1),
          y = $~Functions/value(X = X_2, Y = Y_2, f = φ_2, x = x_2)
        )
      )
    ),
    %equals(
      $~composition(
        X = $~"Cartesian product"(S = X_1, T = X_2),
        Y = X_3, Z = Y_3, g = φ_3, f = ∗
      ),
      $~composition(
        X = $~"Cartesian product"(S = X_1, T = X_2),
        Y = $~"Cartesian product"(S = Y_1, T = Y_2),
        Z = Y_3, g = ⋆,
        f = $~Functions/"cartesian product"(V = X_1, W = Y_1, X = X_2, Y = Y_2, f = φ_1, g = φ_2)
      )
    )
  ]
}
