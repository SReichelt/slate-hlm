%../../../../../logics/hlm%

[
  $~"Cartesian product" = $../../../Essentials/Sets/"Cartesian product",
  $~Functions = $../../../Essentials/Functions,
  $~"Inner operations" = $../../../Essentials/Operations/"Inner operations",
  $~Operations = $../../../Essentials/Operations/Operations,
  $~composition = $../../../Essentials/Functions/composition
]

$"inner operation condition"(
  X,Y: %Set(auto = %true),
  φ: %Element($~Functions/Bijections(X = X, Y = Y)),
  ∗: %Element($~"Inner operations"(X = X)),
  ⋆: %Element($~"Inner operations"(X = Y))
): %Predicate {
  notation = $SpecificIsomorphismRelation(
    operands = [∗, ⋆],
    function = φ
  ),
  definition = [
    %structural(
      ⋆,
      $~Operations(X = Y, Y = Y, Z = Y),
      [{
        constructor = $~Operations.operation,
        parameters = #(_2: %Binder(
          #(
            y_1: %Element(Y),
            y_2: %Element(Y)
          ),
          #(b: %Element(Y))
        )),
        value = %structural(
          ∗,
          $~Operations(X = X, Y = X, Z = X),
          [{
            constructor = $~Operations.operation,
            parameters = #(_1: %Binder(
              #(
                x_1: %Element(X),
                x_2: %Element(X)
              ),
              #(a: %Element(X))
            )),
            value = %forall(
              #(x_1,x_2: %Element(X)),
              %equals(
                $~Functions/value(
                  X = X, Y = Y, f = φ,
                  x = a[x_1 = x_1, x_2 = x_2]
                ),
                b[
                  y_1 = $~Functions/value(X = X, Y = Y, f = φ, x = x_1),
                  y_2 = $~Functions/value(X = X, Y = Y, f = φ, x = x_2)
                ]
              )
            )
          }]
        )
      }]
    ),
    %forall(
      #(x_1,x_2: %Element(X)),
      %equals(
        $~Functions/value(
          X = X, Y = Y, f = φ,
          x = $../../../Essentials/Operations/value(X = X, Y = X, Z = X, ∗ = ∗, x = x_1, y = x_2)
        ),
        $../../../Essentials/Operations/value(
          X = Y, Y = Y, Z = Y, ∗ = ⋆,
          x = $~Functions/value(X = X, Y = Y, f = φ, x = x_1),
          y = $~Functions/value(X = X, Y = Y, f = φ, x = x_2)
        )
      )
    ),
    %equals(
      $~composition(
        X = $~"Cartesian product"(S = X, T = X),
        Y = X, Z = Y, g = φ, f = ∗
      ),
      $~composition(
        X = $~"Cartesian product"(S = X, T = X),
        Y = $~"Cartesian product"(S = Y, T = Y),
        Z = Y, g = ⋆,
        f = $~Functions/"cartesian product"(V = X, W = Y, X = X, Y = Y, f = φ, g = φ)
      )
    )
  ]
}
