%../../../../../../../logics/hlm%

[
  $~Carrier = $../Carrier,
  $~Essentials = $../../../../../Essentials,
  $~"ordinal-indexed sequence length" = $../../../../../Essentials/Sequences/"ordinal-indexed sequence length"
]

/**
 * @references * https://en.wikipedia.org/wiki/Linear_independence
 *             * https://mathworld.wolfram.com/LinearlyDependentVectors.html
 *             * https://proofwiki.org/wiki/Definition:Linearly_Dependent
 *             * https://ncatlab.org/nlab/show/linearly+independent+subset
 */
$"linearly dependent"(
  𝐑: %Element(
    $../../../Rings,
    auto = %true
  ),
  𝐌: %Element(
    $../"Right modules"(𝐑 = 𝐑),
    auto = %true
  ),
  𝒙: %Element($"Element sequences"(𝐑 = 𝐑, 𝐌 = 𝐌))
): %Predicate {
  notation = $Property(
    operand = 𝒙,
    property = %neg('linearly dependent', 'linearly independent')
  ),
  definition = [%exists(
    #(𝐚: %Element($~Essentials/Sets/"Relative complement"(
      U = $Coordinates(𝐑 = 𝐑, 𝐌 = 𝐌, 𝒙 = 𝒙),
      S = $Coordinates(𝐑 = 𝐑, 𝐌 = 𝐌, 𝒙 = 𝒙),
      T = %enumeration($~Essentials/Sequences/Tuples(
          S = $../../../Carrier(𝐑 = 𝐑),
          α = $~"ordinal-indexed sequence length"(
            S = $~Carrier(𝐑 = 𝐑, 𝐌 = 𝐌),
            𝒂 = 𝒙
          )
        ).sequence(_1 = {
          #(k: %Element($~Essentials/Numbers/Ordinal/Subsets/"Segment (less)"(α = $~"ordinal-indexed sequence length"(
            S = $~Carrier(𝐑 = 𝐑, 𝐌 = 𝐌),
            𝒂 = 𝒙
          )))),
          {a = $../../../zero(𝐑 = 𝐑)}
      }))
    ))),
    formula = %equals(
      $"linear combination"(𝐑 = 𝐑, 𝐌 = 𝐌, 𝒙 = 𝒙, 𝐚 = 𝐚),
      $../zero(𝐑 = 𝐑, 𝐌 = 𝐌)
    )
  )]
}
