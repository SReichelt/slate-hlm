%../../../../../../../logics/hlm%

[
  $~Carrier = $../../../Carrier,
  $~"Segment (less)" = $../../../../../Essentials/Numbers/Ordinal/Subsets/"Segment (less)",
  $~Sets = $../../../../../Essentials/Sets,
  $~Tuples = $../../../../../Essentials/Sequences/Tuples
]

/**
 * @remarks This defines the set of valid coordinate sequences corresponding to a given element sequence `𝒙`. This set only depends on the length of `𝒙`. A coordinate sequence is a sequence of ring elements that has the same length and finite support.
 *
 *          For an element sequence `𝒙` and a corresponding coordinate sequence  `𝐚`, we can define a linear combination `$"linear combination"(𝐑 = 𝐑, 𝐌 = 𝐌, 𝒙 = 𝒙, 𝐚 = 𝐚)`.
 */
$Coordinates(
  𝐑: %Element(
    $../../../Rings,
    auto = %true
  ),
  𝐌: %Element(
    $../"Left modules"(𝐑 = 𝐑),
    auto = %true
  ),
  𝒙: %Element($"Element sequences"(𝐑 = 𝐑, 𝐌 = 𝐌)),
  α: %Def($../../../../../Essentials/Sequences/"ordinal-indexed sequence length"(
    S = $../Carrier(𝐑 = 𝐑, 𝐌 = 𝐌),
    𝒂 = 𝒙
  ))
): %SetOperator {
  notation = $SubSup(body = 'Coord', sub = 𝒙),
  definitionNotation = {
    parameter = #(𝐚: %Expr),
    singularName = ['coordinate sequence', ' for ', 𝒙],
    pluralName = ['coordinate sequences', ' for ', 𝒙]
  },
  definition = [%subset(
    #(𝐚: %Element($~Tuples(
      S = $~Carrier(𝐑 = 𝐑),
      α = α
    ))),
    %structural(
      𝐚,
      $~Tuples(
        S = $~Carrier(𝐑 = 𝐑),
        α = α
      ),
      [{
        constructor = $~Tuples.sequence,
        parameters = #(_1: %Binder(
          #(k: %Element($~"Segment (less)"(α = α))),
          #(a: %Element($~Carrier(𝐑 = 𝐑)))
        )),
        value = %exists(
          #(S: %Subset($~"Segment (less)"(α = α))),
          formula = %and(
            $~Sets/finite(S = S),
            %forall(
              #(j: %Element($~Sets/"Relative complement"(
                U = $~"Segment (less)"(α = α),
                S = $~"Segment (less)"(α = α),
                T = S
              ))),
              %equals(
                a[k = j],
                $../../../zero(𝐑 = 𝐑)
              )
            )
          )
        )
      }]
    )
  )]
}
