%../../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Isomorphism
 *             * https://proofwiki.org/wiki/Definition:Isomorphism_(Abstract_Algebra)
 *             * https://ncatlab.org/nlab/show/isomorphism
 */
$Isomorphisms(𝐒,𝐓: %Element($../"Pointed sets")): %SetOperator {
  notation = $Function(
    function = 'Iso',
    arguments = [𝐒, 𝐓]
  ),
  definitionNotation = {
    parameter = #(φ: %Expr),
    notation = $FunctionDeclaration(
      function = φ,
      symbol = '↔',
      operands = [𝐒, 𝐓]
    ),
    singularName = 'isomorphism',
    pluralName = 'isomorphisms'
  },
  definition = [
    %subset(
      #(φ: %Element($Homomorphisms(𝐒 = 𝐒, 𝐓 = 𝐓))),
      $../../../Essentials/Functions/bijective(
        X = $../Carrier(𝐒 = 𝐒),
        Y = $../Carrier(𝐒 = 𝐓),
        f = φ
      )
    ),
    %subset(
      #(f: %Element($../../../Essentials/Functions/Bijections(
        X = $../Carrier(𝐒 = 𝐒),
        Y = $../Carrier(𝐒 = 𝐓)
      ))),
      %equals(
        $../../../Essentials/Functions/value(
          X = $../Carrier(𝐒 = 𝐒),
          Y = $../Carrier(𝐒 = 𝐓),
          f = f,
          x = $../"base point"(𝐒 = 𝐒)
        ),
        $../"base point"(𝐒 = 𝐓)
      )
    )
  ]
}
