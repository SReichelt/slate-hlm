%../../../../logics/hlm%

/**
 * @remarks This definition lets us multiply elements without decomposing the magma.
 */
$operation(
  𝐌: %Element(
    $Magmas,
    auto = %true
  ),
  a: %Element($Carrier(𝐌 = {𝐌})),
  b: %Element(%previous)
): %ExplicitOperator {
  notation = [$Operator(
    symbol = '•',
    operands = [a, b]
  )],
  definition = [%structuralCases(
    𝐌,
    $Magmas,
    [{
      constructor = $Magmas.magma,
      parameters = #(
        M: %Set,
        ∗: %Element($../../Essentials/Functions/Operations/"Inner operations"(X = {M}))
      ),
      value = $../../Essentials/Functions/Operations/value(
        X = {M},
        Y = {M},
        Z = {M},
        ∗ = {∗},
        x = {a},
        y = {b}
      )
    }]
  )]
}
