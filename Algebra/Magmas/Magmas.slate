%../../../../logics/hlm%

/**
 * @remarks Strictly speaking, the elements of this set are not magmas but isomorphism classes of magmas. Due to the rules of HLM, it is not even possible to define magmas (or other structures) in a way that would distinguish two isomorphic structures. In particular, the requirement to specify an equality definition, together with the way two arbitrary sets are treated as separate types, make sure that in the most general case, equality is in fact the same as isomorphism. (It is possible to identify more structures, but not fewer.)
 *
 * @references * https://en.wikipedia.org/wiki/Magma_(algebra)
 */
$Magmas: %Construction {
  $magma(
    M: %Set,
    ∗: %Element($../../Essentials/Functions/Operations/"Inner operations"(X = {M}))
  ): %Constructor {
    display = [$Tuple(
      items = [M, ∗],
      style = '[]'
    )],
    equalityDefinition = {
      leftParameters = #(
        M: %Set,
        ∗: %Element($../../Essentials/Functions/Operations/"Inner operations"(X = {M}))
      ),
      rightParameters = #(
        N: %Set,
        ⋆: %Element($../../Essentials/Functions/Operations/"Inner operations"(X = {N}))
      ),
      definition = [%exists(
        #(φ: %Element($../../Essentials/Functions/Bijections(
          X = {M},
          Y = {N}
        ))),
        formula = %equals(
          $../Isomorphisms/"inner operation transform"(
            X = {M},
            Y = {N},
            φ = {φ},
            ∗ = {∗}
          ),
          ⋆
        )
      )],
      isomorphic = %true
    }
  }

  display = ['𝓜𝓪𝓰'],
  definitionDisplay = {
    parameter = #(𝐌: %Expr),
    singularName = 'magma',
    pluralName = 'magmas'
  }
}
