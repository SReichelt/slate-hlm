%../../../../../logics/hlm%

/**
 * @remarks This is an inductive definition of binary trees with leaves labeled by elements of `S`. It does not really belong in this section but is required for the definition of a free magma (see `$"free magma"`).
 *
 * @references * https://en.wikipedia.org/wiki/Binary_tree
 *             * https://mathworld.wolfram.com/BinaryTree.html
 *             * https://proofwiki.org/wiki/Definition:Binary_Tree
 */
$"Binary trees"(S: %Set(
  auto = %true,
  embedSubsets = %true
)): %Construction {
  $leaf(s: %Element(S)): %Constructor {
    notation = $Parens(
      body = s,
      style = '()'
    ),
    equalityDefinition = {
      leftParameters = #(s: %Element(S)),
      rightParameters = #("s'": %Element(S)),
      definition = [%equals(s, "s'")],
      isomorphic = %true
    }
  }

  $join(a,b: %Element($"Binary trees"(S = S))): %Constructor {
    notation = $Operator(
      symbol = '^',
      operands = [a, b],
      space = ''
    ),
    equalityDefinition = {
      leftParameters = #(a,b: %Element($"Binary trees"(S = S))),
      rightParameters = #("a'","b'": %Element($"Binary trees"(S = S))),
      definition = [%and(
        %equals(a, "a'"),
        %equals(b, "b'")
      )],
      isomorphic = %true
    }
  }

  notation = $Function(
    function = 'ğ“‘ğ“²ğ“·ğ“£ğ“»ğ“®ğ“®',
    arguments = [S]
  ),
  embedding = {
    parameter = #(x: %Element(S)),
    target = $"Binary trees"(S = S).leaf(s = x)
  }
}
