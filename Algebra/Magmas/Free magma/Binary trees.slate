%../../../../../logics/hlm%

/**
 * @remarks This is an inductive definition of binary trees with leaves labeled by elements of `S`. It does not really belong in this section but is required for the definition of a free magma (see `$"free magma"`).
 *
 * @references * https://en.wikipedia.org/wiki/Binary_tree
 *             * https://mathworld.wolfram.com/BinaryTree.html
 *             * https://proofwiki.org/wiki/Definition:Binary_Tree
 */
$"Binary trees"(S: %Set(
  auto = %true,
  embedSubsets = %true
)): %Construction {
  $leaf(s: %Element(S)): %Constructor {
    display = [$Parens(
      body = s,
      style = '()'
    )],
    equalityDefinition = {
      leftParameters = #(s: %Element(S)),
      rightParameters = #("s'": %Element(%previous)),
      definition = [%equals(s, "s'")],
      isomorphic = %true
    }
  }

  $join(
    a: %Element($"Binary trees"(S = {S})),
    b: %Element(%previous)
  ): %Constructor {
    display = [$Operator(
      symbol = '^',
      operands = [a, b],
      space = ''
    )],
    equalityDefinition = {
      leftParameters = #(
        a: %Element($"Binary trees"(S = {S})),
        b: %Element(%previous)
      ),
      rightParameters = #(
        "a'": %Element(%previous),
        "b'": %Element(%previous)
      ),
      definition = [%and(
        %equals(a, "a'"),
        %equals(b, "b'")
      )],
      isomorphic = %true
    }
  }

  display = [$Function(
    function = '𝓑𝓲𝓷𝓣𝓻𝓮𝓮',
    arguments = [S]
  )],
  embedding = {
    parameter = #(x: %Element(S)),
    target = $"Binary trees"(S = {S}).leaf(s = {x})
  }
}
