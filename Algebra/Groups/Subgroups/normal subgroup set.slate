%../../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Normal_subgroup
 *             * https://mathworld.wolfram.com/NormalSubgroup.html
 *             * https://proofwiki.org/wiki/Definition:Normal_Subgroup
 *             * https://ncatlab.org/nlab/show/normal+subgroup
 *             * https://leanprover-community.github.io/mathlib_docs/group_theory/subgroup.html#normal_subgroup
 */
$"normal subgroup set"(
  𝐆: %Element($../Groups),
  S: %Subset($../Carrier(𝐆 = {𝐆}))
): %Predicate {
  notation = [$NounRelation(
    operands = [S, 𝐆],
    singular = 'normal subgroup',
    plural = 'normal subgroups'
  )],
  definition = [
    %and(
      $"subgroup set"(
        𝐆 = {𝐆},
        S = {S}
      ),
      %forall(
        #(
          s: %Element(S),
          g: %Element($../Carrier(𝐆 = {𝐆}))
        ),
        %in(
          $../operation(
            𝐆 = {𝐆},
            a = {%associative($../operation(
              𝐆 = {𝐆},
              a = {g},
              b = {s}
            ))},
            b = {$../inverse(
              𝐆 = {𝐆},
              a = {g}
            )}
          ),
          S
        )
      )
    ),
    %and(
      $"subgroup set"(
        𝐆 = {𝐆},
        S = {S}
      ),
      %forall(
        #(
          s: %Element(S),
          h: %Element($../Carrier(𝐆 = {𝐆}))
        ),
        %in(
          $../operation(
            𝐆 = {𝐆},
            a = {$../inverse(
              𝐆 = {𝐆},
              a = {h}
            )},
            b = {%associative($../operation(
              𝐆 = {𝐆},
              a = {s},
              b = {h}
            ))}
          ),
          S
        )
      )
    ),
    %exists(
      #(
        𝐇: %Element($../Groups),
        φ: %Element($../Homomorphisms/Homomorphisms(
          𝐆 = {𝐆},
          𝐇 = {𝐇}
        ))
      ),
      formula = %setEquals(
        S,
        $../Homomorphisms/Kernel(
          𝐆 = {𝐆},
          𝐇 = {𝐇},
          φ = {φ}
        )
      )
    )
  ]
}
