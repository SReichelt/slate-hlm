%../../../../logics/hlm%

$"normal subgroup set"(
  𝐆: %Element($Groups),
  S: %Subset($Carrier(𝐆 = {𝐆}))
): %Predicate {
  display = [$NounRelation(
    operands = [S, 𝐆],
    singular = 'normal subgroup',
    plural = 'normal subgroups'
  )],
  definition = [
    %and(
      $"subgroup set"(
        𝐆 = {𝐆},
        S = {S}
      ),
      %forall(
        #(
          s: %Element(S),
          g: %Element($Carrier(𝐆 = {𝐆}))
        ),
        %in(
          $operation(
            𝐆 = {𝐆},
            a = {$operation(
              𝐆 = {𝐆},
              a = {g},
              b = {s}
            )},
            b = {$"inverse element"(
              𝐆 = {𝐆},
              a = {g}
            )}
          ),
          S
        )
      )
    ),
    %exists(
      #(
        𝐇: %Element($Groups),
        φ: %Element($Homomorphisms(
          𝐆 = {𝐆},
          𝐇 = {𝐇}
        ))
      ),
      formula = %setEquals(
        S,
        $Kernel(
          𝐆 = {𝐆},
          𝐇 = {𝐇},
          φ = {φ}
        )
      )
    )
  ]
}
