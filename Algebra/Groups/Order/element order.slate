%../../../../../logics/hlm%

$"element order"(
  𝐆: %Element(
    $../Groups,
    auto = %true
  ),
  g: %Element($../Carrier(𝐆 = {𝐆})),
  S: %SetDef(%subset(
    #(n: %Element($../../../Essentials/Numbers/Natural/Subsets/"Positive numbers")),
    %equals(
      $../"power to natural number"(
        𝐆 = {𝐆},
        a = {g},
        n = {n}
      ),
      $../identity(𝐆 = {𝐆})
    )
  ))
): %ExplicitOperator {
  display = [$Parens(
    body = g,
    style = '||'
  )],
  definition = [
    $../../../Essentials/Numbers/Cardinal/"Cardinal numbers".cardinality(S = {$../Subgroups/"Span of element"(
      𝐆 = {𝐆},
      g = {g}
    )}),
    %cases([
      {
        formula = %not($../../../Essentials/Sets/empty(S = {S})),
        value = $../../../Essentials/Numbers/Natural/minimum(M = {S})
      },
      {
        formula = $../../../Essentials/Sets/empty(S = {S}),
        value = $../../../Essentials/Numbers/Cardinal/Examples/aleph(α = {$../../../Essentials/Numbers/Natural/number(value = 0)})
      }
    ])
  ]
}
