%../../../../../../logics/hlm%

[
  $~"Natural numbers" = $../../../../Essentials/Numbers/Natural/"Natural numbers",
  $~power = $../../../../Essentials/Numbers/Natural/power
]

/**
 * @references * https://proofwiki.org/wiki/Definition:P-Element
 */
$"p-element"(
  𝐆: %Element(
    $../../Groups,
    auto = %true
  ),
  g: %Element($../../Carrier(𝐆 = 𝐆)),
  p: %Element($../../../../Essentials/Numbers/Natural/Prime/"Prime numbers")
): %Predicate {
  notation = $NounProperty(
    operand = g,
    singular = $DependentWord(word = 'element', argument = p),
    plural = $DependentWord(word = 'element', argument = p)
  ),
  definition = [
    %exists(
      #(n: %Element($~"Natural numbers")),
      formula = %equals(
        $../"element order"(𝐆 = 𝐆, g = g),
        $~power(m = p, n = n)
      )
    ),
    %exists(
      #(m: %Element($~"Natural numbers")),
      formula = %equals(
        $../../"power to natural number"(
          𝐆 = 𝐆, a = g,
          n = $~power(m = p, n = m)
        ),
        $../../identity(𝐆 = 𝐆)
      )
    )
  ]
}
