%../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Group_(mathematics)
 *             * https://mathworld.wolfram.com/Group.html
 *             * https://proofwiki.org/wiki/Definition:Group
 *             * https://ncatlab.org/nlab/show/group
 *             * https://leanprover-community.github.io/mathlib_docs/algebra/group/basic.html#group
 */
$group(
  G: %Set,
  ∗: %Element($../../Essentials/Operations/"Inner operations"(X = G)),
  e: %Element(G),
  i: %Element($../../Essentials/Functions/Functions(X = G, Y = G))
): %Predicate {
  notation = $Structure(
    items = [G, ∗, e, i],
    singular = 'group',
    plural = 'groups'
  ),
  definition = [%and(
    $../Monoids/monoid(M = G, ∗ = ∗, e = e),
    %forall(
      #(a: %Element(G)),
      %equals(
        $../../Essentials/Operations/value(
          X = G, Y = G, Z = G, ∗ = ∗, x = a,
          y = $../../Essentials/Functions/value(X = G, Y = G, f = i, x = a)
        ),
        $../../Essentials/Operations/value(
          X = G, Y = G, Z = G, ∗ = ∗,
          x = $../../Essentials/Functions/value(X = G, Y = G, f = i, x = a),
          y = a
        ),
        e
      )
    )
  )]
}
