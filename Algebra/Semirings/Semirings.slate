%../../../../logics/hlm%

[
  $~"Inner operations" = $../../Essentials/Operations/"Inner operations",
  $~Operations = $../../Essentials/Operations/Operations,
  $~"element condition" = $../Generic/Isomorphisms/"element condition",
  $~"inner operation condition" = $../Generic/Isomorphisms/"inner operation condition"
]

/**
 * @references * https://en.wikipedia.org/wiki/Semiring
 *             * https://proofwiki.org/wiki/Definition:Rig#Also_defined_as
 *             * https://ncatlab.org/nlab/show/rig
 *             * https://coq.inria.fr/library/Coq.setoid_ring.Ring_theory.html#semi_ring_theory
 *             * https://leanprover-community.github.io/mathlib_docs/algebra/ring/basic.html#semiring
 */
$Semirings: %Construction {
  $semiring(
    R: %Set,
    ⊕: %Element($~"Inner operations"(X = R)),
    "0": %Element(R),
    ⊙: %Element($~"Inner operations"(X = R)),
    "1": %Element(R),
    _1: %Constraint($semiring(R = R, ⊕ = ⊕, "0" = "0", ⊙ = ⊙, "1" = "1"))
  ): %Constructor {
    notation = $Tuple(
      items = [R, ⊕, "0", ⊙, "1"],
      style = '[]'
    ),
    equalityDefinition = {
      leftParameters = #(
        R: %Set,
        ⊕: %Element($~"Inner operations"(X = R)),
        "0": %Element(R),
        ⊙: %Element($~"Inner operations"(X = R)),
        "1": %Element(R),
        _1: %Constraint($semiring(R = R, ⊕ = ⊕, "0" = "0", ⊙ = ⊙, "1" = "1"))
      ),
      rightParameters = #(
        S: %Set,
        ⋆: %Element($~"Inner operations"(X = S)),
        𝟢: %Element(S),
        ∗: %Element($~"Inner operations"(X = S)),
        𝟣: %Element(S),
        _2: %Constraint($semiring(R = S, ⊕ = ⋆, "0" = 𝟢, ⊙ = ∗, "1" = 𝟣))
      ),
      definition = [%exists(
        #(φ: %Element($../../Essentials/Functions/Bijections(X = R, Y = S))),
        formula = %and(
          $~"inner operation condition"(X = R, Y = S, φ = φ, ∗ = ⊕, ⋆ = ⋆),
          $~"element condition"(X = R, Y = S, φ = φ, x = "0", y = 𝟢),
          $~"inner operation condition"(X = R, Y = S, φ = φ, ∗ = ⊙, ⋆ = ∗),
          $~"element condition"(X = R, Y = S, φ = φ, x = "1", y = 𝟣)
        )
      )],
      isomorphic = %true
    }
  }

  notation = '𝓢𝓮𝓶𝓲𝓡𝓲𝓷𝓰',
  definitionNotation = {
    parameter = #(𝐑: %Expr),
    singularName = 'semiring',
    pluralName = 'semirings'
  },
  rewrite = {
    parameter = #(𝐑: %Element($Semirings)),
    value = $Semirings.semiring(
      R = $Carrier(𝐑 = 𝐑),
      ⊕ = $~Operations(
          X = $Carrier(𝐑 = 𝐑),
          Y = $Carrier(𝐑 = 𝐑),
          Z = $Carrier(𝐑 = 𝐑)
        ).operation(_1 = {
          #(a,b: %Element($Carrier(𝐑 = 𝐑))),
          {z = $sum(𝐑 = 𝐑, a = a, b = b)}
      }),
      "0" = $zero(𝐑 = 𝐑),
      ⊙ = $~Operations(
          X = $Carrier(𝐑 = 𝐑),
          Y = $Carrier(𝐑 = 𝐑),
          Z = $Carrier(𝐑 = 𝐑)
        ).operation(_1 = {
          #(a,b: %Element($Carrier(𝐑 = 𝐑))),
          {z = $product(𝐑 = 𝐑, a = a, b = b)}
      }),
      "1" = $one(𝐑 = 𝐑)
    )
  }
}
