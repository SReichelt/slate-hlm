%../../../../logics/hlm%

/**
 * @references * https://en.wikipedia.org/wiki/Semiring
 *             * https://proofwiki.org/wiki/Definition:Rig#Also_defined_as
 *             * https://ncatlab.org/nlab/show/rig
 *             * https://coq.inria.fr/library/Coq.setoid_ring.Ring_theory.html#semi_ring_theory
 *             * https://leanprover-community.github.io/mathlib_docs/core/init/algebra/ring.html#semiring
 */
$Semirings: %Construction {
  $semiring(
    R: %Set,
    ⊕: %Element($../../Essentials/Operations/"Inner operations"(X = {R})),
    "0": %Element(R),
    ⊙: %Element($../../Essentials/Operations/"Inner operations"(X = {R})),
    "1": %Element(R),
    _1: %Constraint($semiring(
      R = {R},
      ⊕ = {⊕},
      "0" = {"0"},
      ⊙ = {⊙},
      "1" = {"1"}
    ))
  ): %Constructor {
    notation = $Tuple(
      items = [R, ⊕, "0", ⊙, "1"],
      style = '[]'
    ),
    equalityDefinition = {
      leftParameters = #(
        R: %Set,
        ⊕: %Element($../../Essentials/Operations/"Inner operations"(X = {R})),
        "0": %Element(R),
        ⊙: %Element($../../Essentials/Operations/"Inner operations"(X = {R})),
        "1": %Element(R),
        _1: %Constraint($semiring(
          R = {R},
          ⊕ = {⊕},
          "0" = {"0"},
          ⊙ = {⊙},
          "1" = {"1"}
        ))
      ),
      rightParameters = #(
        S: %Set,
        ⋆: %Element($../../Essentials/Operations/"Inner operations"(X = {S})),
        𝟢: %Element(S),
        ∗: %Element($../../Essentials/Operations/"Inner operations"(X = {S})),
        𝟣: %Element(S),
        _2: %Constraint($semiring(
          R = {S},
          ⊕ = {⋆},
          "0" = {𝟢},
          ⊙ = {∗},
          "1" = {𝟣}
        ))
      ),
      definition = [%exists(
        #(φ: %Element($../../Essentials/Functions/Bijections(
          X = {R},
          Y = {S}
        ))),
        formula = %and(
          %equals(
            $../Generic/Isomorphisms/"inner operation transport"(
              X = {R},
              Y = {S},
              φ = {φ},
              ∗ = {⊕}
            ),
            ⋆
          ),
          %equals(
            $../Generic/Isomorphisms/"element transport"(
              X = {R},
              Y = {S},
              φ = {φ},
              x = {"0"}
            ),
            𝟢
          ),
          %equals(
            $../Generic/Isomorphisms/"inner operation transport"(
              X = {R},
              Y = {S},
              φ = {φ},
              ∗ = {⊙}
            ),
            ∗
          ),
          %equals(
            $../Generic/Isomorphisms/"element transport"(
              X = {R},
              Y = {S},
              φ = {φ},
              x = {"1"}
            ),
            𝟣
          )
        )
      )],
      isomorphic = %true
    }
  }

  notation = '𝓢𝓮𝓶𝓲𝓡𝓲𝓷𝓰',
  definitionNotation = {
    parameter = #(𝐑: %Expr),
    singularName = 'semiring',
    pluralName = 'semirings'
  }
}
