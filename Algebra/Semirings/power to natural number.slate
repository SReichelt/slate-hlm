%../../../../logics/hlm%

[
  $~"Natural numbers" = $../../Essentials/Numbers/Natural/"Natural numbers"
]

/**
 * @references * https://coq.inria.fr/library/Coq.setoid_ring.Ring_theory.html#pow_N
 *             * https://leanprover-community.github.io/mathlib_docs/algebra/group_power/basic.html#monoid.pow
 */
$"power to natural number"(
  𝐑: %Element($Semirings, auto = %true),
  a: %Element($Carrier(𝐑 = 𝐑)),
  n: %Element($~"Natural numbers")
): %ExplicitOperator {
  notation = $SubSup(body = a, sup = n),
  definition = [%structuralCases(
    n, $~"Natural numbers",
    [
      {
        constructor = $~"Natural numbers".zero,
        value = $one(𝐑 = 𝐑),
        rewrite = %true
      },
      {
        constructor = $~"Natural numbers".successor,
        parameters = #(x: %Element($~"Natural numbers")),
        value = $product(
          𝐑 = 𝐑,
          a = $"power to natural number"(𝐑 = 𝐑, a = a, n = x),
          b = a
        ),
        rewrite = %true
      }
    ]
  )]
}
